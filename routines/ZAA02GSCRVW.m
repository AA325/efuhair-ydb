ZAA02GSCRVW ;PG&A,ZAA02G-SCRIPT,2.10,ZAA02G-VIEW;07NOV94  21:24;;;Fri, 17 Oct 2008  14:38
C ;Copyright (C) 1991, Patterson, Gray & Associates, Inc.
 N:1 (FIND,ZAA02G,ZAA02GP,FDOC,CDOC,DOC,K,TOPP,CAT,VAR,MC,DONE,ZAA02GSCR,INQ,DUZ,DUZC,Timeout,l) S:$D(K)#2 OLDK=K X:$D(VAR) VAR
 D:$D(ZAA02G("ECHO-ON"))+$D(XX)'=2 SETUP^ZAA02GWP S ZAA02G("MENU")=0
 D:$G(TOPP)<2 HEAD X ^ZAA02G("ECHO-OFF")
 I '$D(TOPP) S TOPP=3
DIS S TL=TOPP+2,TOP=TOPP,(INX,BLANK)="" D SETUP S:'$D(CAT) CAT="ALL^ZAA02GSCRTR" D @CAT  I $D(FIND) S $P(FIND,",",3)=-$P(FIND,",",3)
 I $D(INX)>2 D PR0^ZAA02GSCRER2 K INX G DIS
 S ZAA02GWPD=ZAA02GSCR_"(""TRANS"",DOC)",ZAA02GWPB=ZAA02GWPD G:'DOC END G:$D(DONE) END
 ; D BUSY D:DOC PROTECT G:DOC="" DIS
 D DIS1
 I $D(^ZAA02GSCR("PARAM","ALT ZAA02G-VIEW MENU")) X ^("ALT ZAA02G-VIEW MENU")
 ;K NOFAX I $P($G(@ZAA02GWPD),"`",13)'=$TR($P($G(@ZAA02GWPD),"`",13),"EI") S NOFAX=1 ; IN ALT ZAA02G-VIEW MENU
 K Y S Y=",24\QUHP5\*\\\"_$S($D(AUT):"AUTHENTICATE,",1:"")_"PRINT REPORT,"_$S($D(^ZAA02GFAXC)&'$D(NOFAX):"FAX,",1:"")_"EXIT",Y(0)="\EX",Y=$L($P(Y,"\",6),",")*5+$L($P(Y,"\",6))*-1+80\2_Y
 S %R=24,%C=1 W @ZAA02GP,ZAA02G("CL") D ^ZAA02GPOP S:$D(OLDK) K=OLDK G DIS:X["EX" S X=$E($P($P(Y,"\",6),",",X)),S=1 G PRINT:X="P",FAX:X="F",AUTH^ZAA02GSCRVW1:X="A",DIS
DIS1 I ZAA02GWPD["^ZAA02GSCR",$D(@ZAA02GWPD) S %R=TOPP,%C=1,j=^(DOC),II=10000000-DOC,SDIR="A" X:$D(YXL) YXL W @ZAA02GP,ZAA02G("CL")," ",ZAA02G("RON"),$$LEV4^ZAA02GSCRTR(j),ZAA02G("ROF") K II,SDIR
 I $D(@ZAA02GSCR@("TRANS",DOC))<11 D ARCH^ZAA02GSCRAT G END:DOC="",END:'$D(@ZAA02GSCR@("TRANS",DOC))
 ;
 S WO="",MG=$S($G(@ZAA02GWPD@(.01)):^(.01),1:76),NM=DOC
 S UP="ABCDEFGHIJKLMNOPQRSTUVWXYZ",LC="abcdefghijklmnopqrstuvwxyz"
 S DT="",REBUILD=1 D FETCH^ZAA02GSCRER
 S ZAA02GWPOPT="NC,AT,IM,OT1,2,3,4,5,6,8,9,10,14,15,16,18"_$G(INQ,"INQ")
 S HLPR="^ZAA02GSCRH",AST="*",DIR="TRANS" D ENTRY^ZAA02GSCRER Q
 ;
END S %R=TOPP,%C=1 W @ZAA02GP,ZAA02G("CS") L  S:$D(OLDK) K=OLDK Q
PRINT K DISTR,INP I ZAA02GWPB["ZAA02GSCR" S:$G(@ZAA02GSCR@("PARAM","AI-DISTR"))]"" DISTR=^("AI-DISTR") S PRINT=1 D PR1^ZAA02GSCRER2 K PRINT,INP G DIS
FAX I $D(^ZAA02GSCR("PARAM","ALT ZAA02G-VIEW FAX")) X ^("ALT ZAA02G-VIEW FAX") G DIS
 S ZAA02GWPD=ZAA02GWPB D FAXAI^ZAA02GSCRFX K INP G DIS
 ;
SETUP I '$D(ZAA02GSCR) S ZAA02GSCR=$S($G(^ZAA02GSCR("PARAM","DATABASE"))]"":^("DATABASE"),1:"^ZAA02GSCR")
 S $P(ZAA02GSCRP,";",3)=2 ; VA=1,ADS=2,MIDS=3
 I $D(@ZAA02GSCR@("PARAM","VIEWTR")) S YX=^("VIEWTR") I $D(^("VIEWLOGIC")) S YXL=^("VIEWLOGIC")
 Q
BUSY I '$TR($P(@ZAA02GSCR@("TRANS",DOC),"`",13),"RHT",111) Q
 S Y="17,10\RL\1\\\Transcript is not available.  Try inquiry*,again in a few minutes.*,*,             PRESS RETURN     " D ^ZAA02GPOP S DOC="" K Y Q
PROTECT Q:'$D(@ZAA02GSCR@("TRANS",DOC,.011))  I $P($G(ZAA02GSCRP),";",3)=1  Q:$P(^(.011),"`",16)'["P"  G PROTVA
 Q:"U"[$P(^(.011),"`",21)  G PROTGEN
PROTVA ; VA confidentiality notice
 N (ZAA02G,ZAA02GP,DOC) S Y="4,10\DVYRL\1\WARNING - RESTRICTED RECORD\\"
 S Y(1)="This record is protected by the Privacy Act of 1974.  If you elect*",Y(2)="to proceed, you will be required to prove you have a need to know.*"
 S Y(3)="Accessing this patient is tracked, and your station Security Officer*",Y(4)="will contact you for your justification.* ",Y(5)="*"
 S Y(6)="RETURN WITHOUT VIEWING RECORD",Y(7)="PROCEED TO VIEW RECORD" D ^ZAA02GPOP S:$E(X)="R" DOC="" Q
 ;         
PROTGEN ; general confidentiality notices
 S X=$P(^(.011),"`",21) I X="L",$G(TRANS)'="" Q
 I X="O",$D(TRANS),TRANS'=$P(^(.011),"`",3) Q:TRTYPE>2
 I X="O",$G(DUZ)'="" Q
NOACCESS N (ZAA02G,ZAA02GP,DOC) S Y="4,10\DVYRL\1\WARNING - RESTRICTED RECORD\\"
 S Y(1)="This record is not available for general access.  Please*",Y(2)="contact the transcription department if you need a copy*"
 S Y(3)="of this record.*",Y(4)="* "
 S Y(5)="Press RETURN to exit" D ^ZAA02GPOP S DOC="" Q
 ; 
LIMITED N (ZAA02G,ZAA02GP,DOC) S Y="4,10\DVYRL\1\WARNING - RESTRICTED RECORD\\"
 S Y(1)="This record has limited access.  If you elect to proceed,*",Y(2)="you will be required to prove you have a need to know.*"
 S Y(3)="Note that the access of this record is tracked.*",Y(4)="* ",Y(5)="*"
 S Y(6)="RETURN WITHOUT VIEWING RECORD",Y(7)="PROCEED TO VIEW RECORD" D ^ZAA02GPOP S:$E(X)="R" DOC="" Q
 ; 
HEAD W ZAA02G("F") S %R=1,%C=2 I $D(ZAA02G("a"))=0 W ZAA02G("LO"),@ZAA02GP,ZAA02G("RON")," P ",ZAA02G("RT")," G ",ZAA02G("RT")," & ",ZAA02G("RT")," A " S %C=69 W @ZAA02GP,"  ZAA02G-VIEW  ",ZAA02G("ROF"),!
 E  S A=$P(ZAA02G("a"),"`"),I=$P(ZAA02G("a"),"`",2),%C=18 W @ZAA02GP,I,*13," ",ZAA02G("HI"),A,"P ",I,A,"G ",I,A,"& ",I,A,"A ",I S %C=70 W @ZAA02GP,A,"ZAA02G-VIEW ",I,!
 S T=$S('$D(AUT):"M E D I C A L   R E P O R T   I N Q U I R Y",1:"R E C O R D   R E V I E W  &  A P P R O V A L")
 W ZAA02G("G1") S J=ZAA02G("HL"),J=J_J_J_J F I=1:1:20 W J
 W ZAA02G("G0") S %C=84-$L(T)\2 W @ZAA02GP,ZAA02G("HI"),T K T
 Q
 ;
PRV S PRV=DUZ,VAR="S PV="""_PRV_" """,CAT="PRV1" G ZAA02GSCRVW
PRV1 S SRC="@ZAA02GSCR@(""DIR"",5,PV)",E="I 1",OF=-1,OF1=10000000,EP="zzzzz" G ^ZAA02GSCRTR
SETM S VAR="S SETM=""4,T""",FDOC=0 G ZAA02GSCRVW
ALLF S FDOC=0
ALL G ZAA02GSCRVW
AUT K DUZC D SETUP S INQ="",B=$P(ZAA02GSCRP,";",3) S DUZC=$G(DUZ) G AUTADS:B=2,AUTC:B=4
 I '$G(DUZ) D ^ASIRESET Q:'$G(DUZ)
 ; D ^ASIES Q:'ESOK  G AUT2
AUTADS D:'$D(ZAA02G) INIT^ZAA02GDEV W ZAA02G("F") W !!,"Enter Electronic Signature Password - " X ^ZAA02G("ECHO-OFF") R X X ^ZAA02G("ECHO-ON") Q:X=""
 S X=$G(@ZAA02GSCR@("PROV",0,X),$G(@ZAA02GSCR@("PARAM","ECODE",X))) I X="" W "  CODE NOT FOUND" H 1 G AUTADS
 I $P(@ZAA02GSCR@("PROV",X),"\",19)'["Y" W "  YOU DON'T CURRENTLY HAVE ES PRIVILEGES" H 1 G AUTADS
 S (DUZ,DUZC,TRANS)=X S INQ=$S($P(^(X),"\",13)="Y":1,1:"") S:$P($G(@ZAA02GSCR@("PARAM","BASIC")),"|",13)="Y" DUZ="ALL"
AUTC ;
AUT2 K VAR,CAT S B="" S:'$D(ZAA02GSCR) ZAA02GSCR="^ZAA02GSCR" F  S B=$O(@ZAA02GSCR@("DIR",48,DUZ,B)) Q:B=""  D:B<0 AUTT I B>0 K ^(B) S ^(-B)=""
 I '$D(@ZAA02GSCR@("DIR",48,DUZ)) W !!!?20,"No Reports Waiting for Authentication",!!?20,"Press RETURN " R CCC Q
 F  S VAR="S SETM=""13,E"",AUT="""_DUZ_"""",CAT="AUT1",Timeout=60 D ZAA02GSCRVW W ZAA02G("F") X ^ZAA02G("TERM-OFF") Q:$G(Timeout)'="Y"
 Q
AUT1 S SRC="@ZAA02GSCR@(""DIR"",48,AUT)",E="S:I<0 II=-I I 1",OF=-1,OF1=10000000,EP="zzzzz" G BEG^ZAA02GSCRTR
AUTT I $D(@ZAA02GSCR@("DIR",12,"H",-B))=0 S A=$G(@ZAA02GSCR@("TRANS",10000000+B,.011,"eS")) I $E(A)'="S" F R=2,3 S K=$P(A,"\",R) F S=1:1:$L(K,",") I $P(K,",",S)=DUZ G AUTTE
 K @ZAA02GSCR@("DIR",48,DUZ,-B)
AUTTE Q
 ;
MR S MR=$TR($G(INP("MR")),"-","") I MR]"" S:+MR=MR MR=MR_" " S VAR="S MR="""_MR_"""",CAT="MR1" G ZAA02GSCRVW
 G ALLF
MR1 S SRC="@ZAA02GSCR@(""DIR"",3,MR)",E="I 10000000-I'=CDOC",OF=-1,OF1=10000000,EP="zzzzz" G ^ZAA02GSCRTR
SITE S VAR="S SETM=""10,"_$G(SITE)_"""",FDOC=0 G ZAA02GSCRVW
DATE D:$D(ZAA02GSCR)>0+$D(XX)'=2 SETUP^ZAA02GSCRPW
 S SCR="ZAA02GSCRIPT",SNL=16,REFRESH="3:22",ZAA02Gform="0;;;N",%R=3 W @ZAA02GP,ZAA02G("CS") D ^ZAA02GFORM Q:ZAA02Gform["TIME"  S DATE=$P(DATE,"/",3)_$P(DATE,"/")_$P(DATE,"/",2)
 S (FP,EP)="",Y=$O(@ZAA02GSCR@("DIR",98,DATE-1)) I Y'="" S EP=10000000-^(Y),Y=$O(^(DATE)) I Y'="" S FP=10000000-^(Y)
 G ALL
 ;
