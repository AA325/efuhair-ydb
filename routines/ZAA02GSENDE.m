ZAA02GSENDE ;PG&A,ZAA02G-SEND,1.36,MODEM TRANSFER (Kermit-RCV);11APR96 9:50A;;;1NOV96 5:20P
 ;PG&A, 1993
TR S ER=0,KP="D",GLOB="^ZAA02GKERMIT($J)",FGLOB="^ZAA02GKERMIT($J)" D INIT^ZAA02GSENDA,REXE Q
 ;
EXIT I ER U MODEM F  R A:1 E  U 0 Q
 S @GLOB@(0,FILE,"STOP")=$H,@GLOB@(0,FILE,"TBYTES")=TBY,@GLOB@(0,FILE,"ERROR")=+$G(ER) K CC,PTR,SP,SN,X,SD,TBY,SBY,WO Q
SEND I $L(SD)<95 S X=$C($L(SD)+35,SN#64+32)_SP_SD,CS=0 X CRC S P=SH_X_$C(CS\64#4+CS#64+32)_SE
 I $L(SD)>94 S X=$C(32,SN#64+32)_SP_$C($L(SD)+1\95+32,$L(SD)+1#95+32),CS=0 X CRC S X=X_$C(CS\64#4+CS#64+32)_SD,CS=0 X CRC S P=SH_X_$C(CS\64#4+CS#64+32)_SE
SEND1 U MODEM H SPS F I=1:1:SA W *SC
 W P Q:KP'["T"  U 0 W !,ZAA02G("HI"),"SENDING PACKET - ",SN,", TYPE - ",SP,"  DATA: ",$TR(P,$C(1,13),""),$S(P[$C(13):"<CR> ",1:" ")
 I WO S I="" W ! F  S I=$O(WO(I)) Q:I=""  W I,"-",$G(WO(I)),"  "
 Q
 ;
REXE S FILE=1,KP=$S(MODEM=0:"",1:KP) D GFILE S @GLOB@(0,FILE,"START")=$H,@GLOB@(0,FILE,"TYPE")="RECEIVE",TM=0 I KP["D" S (PTR,RP)="" D REXD
REXE0 S SD="" D REC G:'F REXE2
REXE1 I KP["T" U 0 W "  ERROR - ",$S(F=4:"ABORT",F=3:"SEQUENCE",F=2:"TIMEOUT",1:"CHECKSUM (EXPECTED "_($A(A,RZ)-32)_" RECEIVED "_CS),!
 S SN=SEQ I WO,F=1 ; S F="" F J=1:1 S F=$O(WO(F)) Q:F=""  I WO(F)'=0 S SN=F Q
 I 'ER S CT=CT+1,TT=TT+1 I CT<NERR S SP="N",SD="" D SEND G REXE0
 D MAXERR:SN,STERR:'SN S SP="E" D SEND G EXIT
REXE2 G:RP'="D" REXE4 I 'WO,SW,SEQ K WO S WO=SW F SN=SEQ:1:SEQ+WO-1 S WO(SN)=""
 G REXE4:'WO S A=$S(RN=SEQ:1,RN<$O(WO("")):4,$O(WO(""))+WO+WO<RN:0,RN>SEQ:2,1:3) G REXE0:'A I A<4,'$D(WO(RN)) S SN=$O(WO("")) F SN=SN:1:RN-WO I $G(WO(SN))=0 K WO(SN)
 S:A=1 SEQ=SEQ+1,LN=$S(SEQ#64=0:LN+64,1:LN) I A=2 S SP="N",SEQ=RN+1 F SN=$O(WO("")):1:RN-1 I $G(WO(SN))'=0 S WO(SN)="" D SEND
 S SN=RN,CT=0 G:A'=4 REXE5 D SEND G REXE0
REXE4 S SN=SEQ S:SN#64'=(RN#64) F=3 S:RP="E" F=0 G REXE1:F S SEQ=SEQ+1,LN=$S(SEQ#64=0:LN+64,1:LN)
REXE5 D:KP["D" REXD S PTR="RB"_RP,RLIN=$T(@PTR) X:$P(RLIN,";",2)]"" $P(RLIN,";",2) I 'F S SP="Y" D SEND G:"RBB,RBE"[PTR EXIT G REXE0
 G REXE1
REC S F=2 I $TR(KP,"TD","")'=KP F I=1:0:RT U 0 R *A:0 G:A>0 TRAP:$C(2,3,24)[$C(A) D:A>0 RECX:"tT"[$C(A) U MODEM R *A:1 Q:$C(A)=RH  S:'$T I=I+1 G:I=RT REC1 I $T,KP["T",A>31 U 0 W *A
 I $TR(KP,"TD","")=KP F I=1:0:RT U MODEM R *A:1 Q:$C(A)=RH  S:'$T I=I+1 G:I=RT REC1
REC0 R *A:RT G REC1:'$T,REC:$C(A)=RH S RZ=A-32,CS=A G:RZ=0 RECL G REC1:RZ<1 R A#RZ:RT G REC1:'$T
REC2 S RN=$A(A)-32,RP=$E(A,2),X=$E(A,1,RZ-1),RD=$E(A,RZ>94*3+3,RZ-1) X CRC S CS=CS\64#4+CS#64,F=$A(A,RZ)-32'=CS,RN=$S('WO:LN+RN,RN>15&(RN<49):LN+RN,1:RN+16#64+LN+(SEQ#64>31*64)-16)
REC1 Q:F  Q:KP'["T"  U 0 W !,ZAA02G("LO"),"RECEIVED PACKET - ",RN,", TYPE - ",RP,"  DATA: ",$TR(A,$C(1,13),""),$S(A[$C(13):"<CR> ",1:"  ") Q
RECX S KP=$TR(KP,"DT","TD") D:KP]"" DISP^ZAA02GSENDA Q
RECL R B#5:RT S X=$E(B,1,4) X CRC S CS=CS\64#4+CS#64,F=$A(B,5)-32'=CS I F G REC1
 S X=$A(B,3)-32*95+$A(B,4)-32,CS=A,RZ=X+5 R X#X:RT S A=B_X I '$T S F=2 G REC1
 G REC2
TRAP S ER=1,F=4 U 0 W *7 Q
RB ; receive batch sequence
RBS ;D RPARM; S
RBF ;D RFILE; F
RBB ;D REOB; B
RBZ ;D REOF; Z
RBD ;D RDATA; D
RBE ;D RERROR; E
RBA ;D RATTR; A
 ;
REXD U 0 D:RP="" DISP^ZAA02GSENDA S %R=12,%C=33 W @ZAA02GP,SBY\1000,"K,",TBY\1000,"K   " S %R=13 W @ZAA02GP,TBY\($P($H,",",2)-TM+.01),"   "
 S %R=15,%C=53 W @ZAA02GP,$J(TT,2) S %C=33 W @ZAA02GP,RN S %C=43 W @ZAA02GP,RP Q
RPARM D RPARM^ZAA02GSENDA,CTINIT Q
 Q
RERROR S ER=3,F=0 I KP["T" U 0 W "ERROR MESSAGE: ",RD,!
 S @GLOB@(0,FILE,"ERRORM")=RD Q
 ;
RDATA S RDB=RN I WO Q:$G(WO(RN))=0  S WO(RN)=0
 G:KP'["N" RQ S TBY=TBY+$L(RD),SBY=SBY+$L(RD),@FGLOB@(FILE,RDB)=RD Q
GFILE F FILE=FILE:1 Q:'$D(@GLOB@(0,FILE))
 Q
RFILE S @GLOB@(0,FILE,"FILE")=RD,@GLOB@(0,FILE,"FILE",0)=FGLOB,WO=0,%R=11,%C=33,SBY=0 U 0 W:KP["D" @ZAA02GP,$E(RD_$J("",20),1,20)
 S:'TM TM=$P($H,",",2) Q
REOB Q
RATTR S @GLOB@(0,FILE,"ATTR")=RD Q
REOF S @GLOB@(0,FILE,"END")=$H Q
MAXERR I ER=1 S SD="CONTROL STOP FROM USER" I 1 Q
 S SD="MAXIMUM ERROR COUNT REACHED",ER=2 I 1 Q
STERR S SD="  TRANSFER TIMEOUT - DIDN'T START IN TIME  ",ER=4 I 1 Q
CLR U MODEM F I=1:1 R A:1 Q:'$T
 Q
CTINIT K CT2 S (CT0,CT1)="",$P(CT1,2,130)="" F J=0:4:126 S CT0=CT0_$C(J,J+1,J+2,J+3)
 F J=S8,SK,SQ I J]"" S j=$A(J),CT1=$E(CT1,1,j)_1_$E(CT1,j+2,255)
 S:S8]"" CT2(S8)="D NRQ8" S:SK]"" CT2(SK)="D NRQR" S CT2(SQ)="D NRQQ" Q
RQ S RC=$TR(RD,CT0,CT1),F=$F(RC,1) I F S (E,x)=0,D=RD,RD=$E(D,1,F-2) F  X CT2($E(D,F-1)) S jj=F,F=$F(RC,1,F) S:F=0 F=$L(D)+2_"," D:F-jj+$L(RD)+$L(j)>256 RQOV S RD=RD_j_$E(D,jj,F-2) Q:F[","
 G RQ4
NRQR S x=$A(D,F)-31,j=$E(D,F+1),F=F+2 G:S8]"" NRQ8:j=S8 G NRQQ:j=SQ,NRQX
NRQ8 S E=128,j=$E(D,F),F=F+1 G NRQQ:j=SQ S j=$C($A(j)+128),E=0 G NRQX
NRQQ S j=$S("?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_"[$C($A(D,F)#128):$C($A(D,F)#128=63*128+$A(D,F)-64+E),1:$C($A(D,F)+E)),F=F+1,E=0
NRQX S:x $P(j,j,x)="",x=0 Q
RQ4 S @FGLOB@(FILE,RDB)=RD,TBY=TBY+$L(RD),SBY=SBY+$L(RD),F=0 Q
LOG S LOG=$G(LOG)+1,^KERML(LOG,1)=i_" "_J,^(2)=E,^(3)=RD Q
RQOV S TBY=TBY+255,SBY=SBY+255 I $L(RD)+$L(j)>255 S @FGLOB@(FILE,RDB)=RD_$E(j,1,255-$L(RD)),RD=$E(j,256-$L(RD),511),j="",RDB=RDB+.01 Q
 S RD=RD_j,j="",@FGLOB@(FILE,RDB)=RD_$E($E(D,jj,F-2),1,255-$L(RD)),jj=255-$L(RD)+jj,RDB=RDB+.01,RD="" Q
RT S SQ="#",(TBY,SBY)=0,FILE=1,FGLOB="FILE",RDB=1 F S8="","&" F SK="","~" D CTINIT f jk=2:1 s RD=$P($T(RT+jk),";",2) Q:RD=""  W !,S8,SK,?4,RD,?40 S RD=$P(RD,"   ") D RQ W $TR(RD,$C(4,5,6),".,;")
 Q   ; need to delete S9 if not used anymore
 ; ab&#~*g      mix
 ; ab#Dde#E     control D,E
 ; ab~*de~"F    repeat D,F
 ; ab#~de#FG    embedded ~
 ; ab~##DeF      repeat control D
 ; ab##de##     embedded #
 ; ab~*&#De     3-types
