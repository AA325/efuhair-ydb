ZAA02GFAXD1 ;PG&A,ZAA02G-FAX,1.36,DEVICE DRIVER;27OCT95 12:56P;;;Tue, 31 May 2011  09:25
 ;Copyright (C) 1991-1994, Patterson, Gray and Associates Inc.
 ;
BEG D DT^ZAA02GFAXH K DATA
 I '$D(NB) S NB="" F J=32:2:255 S NB=NB_$C(J,J+1)
 S NB=$TR(NB,",W;1234567890","")
 S X=^ZAA02GFAXC("CONFIG"),TP=$P(X,"\",4),PGL=$P(X,"\",6),CG=$P(X,"\",3),CCV=$P(X,"\",17) D JOB
ST S ER=1,DD="DATA",DV=FAX,MFR=" ",PTS="" U:DV'=0 DV K BAUD,ERR D SEND G:'OK END
MFR S MFR=$S(MFR["Ever":"E",MFR["Multi-":"M",MFR["Rock":"M",MFR["E-TE":"B",1:"R")
 S ER=1,DD="DATA"_$S($D(EXEC):3,1:1) D SEND
END K BAUD,DT,PGE,PG,RTA,PTS,DATA,SN,X1,PGL,CCV,TP,CG,D8 Q
BAUD S BAUD=$S('$D(BAUD):2400,BAUD=2400:19200,1:0) Q:'BAUD  G SETBAUD^ZAA02GFAXU
 ;
JOB S JOB=$P(JB,",") Q:JOB=""  S JB=$P(JB,",",2,99),ZAA02GFAXD="^ZAA02GFAXI(JOB)"
 K EX,PG S B=^ZAA02GFAXQ("DIR",10000-JOB),DT(2)=$P(B,"\",12),J=$P(B,"\",20),DT(1)=$S(J=4:1,J=7:2,J=1:5,1:3) I $D(^(10000-JOB,0)) S EX=^(0)
 S DT(3)=$P(FAXP,"\")_$TR($P(B,"\",4),NB,""),DT(9)=$P(FAXP,"\",2),CV="Nnpy"'[$P(B,"\",15),PTL="Nn"'[$P(B,"\",18),PGN=$P(@ZAA02GFAXD,",",3),PTS="" I PTL S PTL=PGL*97.8 S:PTL<300 PTL=1075
 S DT(8)=$S($D(HIRES):1,1:0),PG=$S(PTL<1:1100,1:PTL) F B=1:1:PGN I $G(@ZAA02GFAXD@(B))>PG!(^(B)["HIRES") S DT(8)=1
 S PG=1 X:$G(EX)]"" EX Q
 ;
SEND I $G(OS)="M3" S OK=1,(PTS,CSI)="",ST=$H H 2 D:DD'="DATA" FPOST Q
 K ERR F SN=1:1:99 S SCR=$P($T(@DD+SN),";;",2,99) Q:SCR=""  I $P(SCR,"`",7)'[MFR H 0 D INQ X $P(SCR,"`",6-OK) Q:'OK
 Q:OK  W *13 F J=1:1 D ERROR^ZAA02GFAXD2 S JOB=$P(JB,",") Q:JOB=""  S JB=$P(JB,",",2,99)
 Q
XMT S C=$P($H,",",2)
 I DCS="" S DCS=$TR($P($P(DATA,"+FDCS:",2),"\")," ",""),I=$P(DCS,",",2),MIN=20 S:I MIN=$P("0,5,10,10,20,20,40,40",",",$P(DCS,",",8)+1) S MIN=MIN*.0014*$S(I=3:960,I=2:720,I=1:480,I=5:1440,I=4:1200,1:960),MIN=MFR="E"*MIN*.8+MIN\1
 S PAD=$TR($J("",MIN)," ",$C(0)),EL=^ZAA02GFAXC(0,2,1),BL=EL_$E(PAD,1,MIN-$L(EL)),EOP=^(2),TFL=0
 D RSEND S NL=@ZAA02GFAXD@(PG),X=$D(^(PG,0)),X=$D(^(NL)),X=$D(^ZAA02GFAXC("FX",2,1)),(D7,D8)="W $S(B="""":BL,$L(B)<MIN:$E(B_PAD,1,MIN),1:B) R *B:0"
 S D9="W @$S(B="""":""BL,BL"",$L(B)<MIN:""$E(B_PAD,1,MIN),$E(B_PAD,1,MIN)"",1:""B,B"") R *B:0" I DT(8) S D7=D9
 I DT(8),NL'["HIRES" S D8=D9
 I MFR'="M" H:$P($H,",",2)=C 1 F MM=1:1:100 W PAD
 E  F MM=1:1:200 W PAD R *B:0 G:B=24 XMTA
 R A:0 S B=A R A:0 S B=B_A R A:0 G:B_A[$C(24) XMTA W BL I TP,NL'["HIRES" F TFL=2:1:TP-.1*97.8#300 W BL
 I CV,PG=1,$E(CG)="^",$D(@CG) S CC="",TFL=TFL+$S($D(@CG)#2:@CG,1:0),FL=$D(@CG@(0)) F FL=1:1 S CC=$O(^(CC)) Q:CC=""  S B=^(CC) X D8 G:B=24 XMTA
 I CV,PG=1 S CC="",TFL=TFL+46,FL=$D(^ZAA02GFAXC("FX",CCV="W"*4+2,1)) F FL=1:1 S CC=$O(^(CC)) Q:CC=""  S B=^(CC) X D8 G:B=24 XMTA
 S TFL=TFL+NL,X=$D(@ZAA02GFAXD@(PG,0)) F FL=1:1:NL S B=^(FL) X D8 G:B=24 XMTA
 W BL F TFL=TFL+1:1:$S(PTL:PTL-90*(NL["HIRES"+1),1:TFL+25) W BL
 S CC="" F TFL=TFL:1 S CC=$O(^ZAA02GFAXC("FX",1,CC)) Q:CC=""  S B=^(CC) X D7 G:B=24 XMTA
 S B="" X D7
 S X=$D(@ZAA02GFAXD@(0,PG,0)),CC="" F TFL=TFL+2:1 S CC=$O(^(CC)) Q:CC=""  S B=^(CC) X D7 G:B=24 XMTA
 F TFL=TFL:1:TFL+5 W BL
 W BL,PAD,EOP Q
 ;
XMTA I $D(TEST) U 0 W !,"<CAN> received"  U DV
 Q
 ;
NEXT S DT(4)=$S(JB:0,PG<PGN:0,1:2) S:$D(ERR) ER=5,OK=1,SN=SN+1 Q
DIAL S OK=1,ER=3,RTA=0
 F K=1:0:8 R A:10 S:'$T K=K+1 S DATA=$S($L(DATA)+$L(A)>255:$E(DATA,$L(DATA)-29,255),1:DATA)_A_"\" S:DATA["BUSY" ER=6,OK=0 S:DATA["DIALTONE" ER=4,OK=0 S:DATA["FHNG" OK=0 S:DATA["CONNECT" OK=0 Q:'OK  Q:DATA["OK"  Q:'$D(^ZAA02GFAXQ(.9,JOB))
 I $D(TEST) U 0 W !,"D: ",ER," ",DATA,! U DV
 I $D(EXEC) S OK=DATA["CONNECT" Q
 S:OK OK=DATA["OK" W:'OK *13,"ATZ",*13 S:OK CSI=$TR($P($P(DATA,"+FCSI:",2),"\"),""" ",""),ST=$H H 1 Q
 ;
AGAIN S PTSI=$TR($P($P(DATA,"+FPTS:",2),"\")," ",""),PTS=PTSI_","_PTS,RTA=RTA+1
 I 23[+PTSI D RETRAIN
 S:13[+PTSI PG=PG+1,RTA=0 I RTA<3,PG'>PGN G:$D(PG(PG)) AGAIN S SN=SN-3 Q
 S:+PTSI=0 OK=0 S:RTA>2 OK=0 D FPOST,JOB S:JOB SN=SN-3 Q
 ;
RETRAIN Q:'DT(1)  S DT(1)=$E("01233",DT(1))
 S SCR="AT+FDIS=*8,*1`OK`3`2`" D INQ S OK=1
 Q
 ;
EXEC X EXEC D FPOST,JOB G:JOB EXEC Q
 ;
INQ S OK=0,X1=$P(SCR,"`"),DN=$P(SCR,"`",2),IT=$P(SCR,"`",3),T=$P(SCR,"`",4)
INQ2 I X1["*" S X1=$P(X1,"*")_DT($E($P(X1,"*",2)))_$E($P(X1,"*",2,9),2,255) G INQ2
 I $D(TEST) U 0 W "C: ",SCR,! U DV
 H .3
 F J=1:1 R A:0 Q:'$T
 S DATA="" F J=1:1:IT W:X1["$C" @X1 W:X1'["$C" X1,*13 S T1=$P($H,",",2)+T F K=1:1:199 Q:$P($H,",",2)>T1  R A:T X:$D(TEST) "U 0 W ""R: "",A,! U DV" S:$L(DATA)+$L(A)<250 DATA=DATA_A_"\" G INQ1:DATA[DN
 Q
INQ1 S OK=1 Q
 ;
RSEND S B=^ZAA02GFAXQ("DIR",10000-JOB),$P(B,"\",27)=C,$P(B,"\",7)=DTM,$P(B,"\",5)=CSI,$P(B,"\",22)=DCS
 S $P(B,"\",3)="Page-"_PG,^(10000-JOB)=B I JB F B=1:1:$L(JB,",") S X=$P(JB,",",B) I X,$D(^ZAA02GFAXQ("DIR",10000-X)) S $P(^(10000-X),"\",3)="Connect"
 Q
FPOST S B=^ZAA02GFAXQ("DIR",10000-JOB),A=$P($H,",",2)-$P(ST,",",2)+4,ST=$H S:A<0 A=86400-A S SJB(JOB)=A,CC=$TR($P(B,"\",8)," ",""),A=CC*60+$P(CC,":",2)+A,$P(B,"\",8)=$J(A\60_":"_$E(A#60+100,2,3),5),$P(B,"\",21)=$TR(PTS,",",""),$P(B,"\",5)=CSI
 S:OK $P(B,"\",3)="Sent"_$S($P(B,"\",33):"-"_($P(B,"\",33)+1),1:""),$P(B,"\",10)=7 S ^ZAA02GFAXQ("DIR",10000-JOB)=B Q:'OK  K ^(10000-JOB,0),^ZAA02GFAXQ(.9,JOB)
 Q
 ;
 ;;; D BAUD S:J'=IT SN=SN-2,OK=1
DATA ;;
 ;;ATHE1V1Q0`OK`3`1``
 ;;ATHE1V1Q0`OK`2`1
 ;;AT+FMFR?`OK`3`1`S MFR=DATA,SN=5 I DATA["ROCK"+(DATA["Rock")+(DATA["Ev")+(DATA["Mult")+(DATA["E-TE")+(DATA["Prac")=0 S SN=3
 ;;AT`OK`2`2
 ;;
DATA1 ;;
 ;;ATHE1V1`OK`2`2
 ;;ATV1M*9L1X4S7=120S8=2`OK`2`2
 ;;AT\Q1\X1`OK`2`2```RMB
 ;;AT+FCLASS=2`OK`2`2`
 ;;AT+FDCC=?`OK`2`2`S FDCC=DATA I DT(1)=5,$P(FDCC,"(",3)'[5 S DT(1)=3
 ;;AT+FDIS=*8,*1`OK`3`2`S DCS=""`S DT(1)=$E(333213,$F(5321,DT(1))+1)
 ;;AT+FLID="*2"`OK`2`2`H 2`
 ;;*3`OK`1`1``D DIAL
 ;;AT+FDT`CONNECT`1`80`D XMT`S ER=2
 ;;$C(16,3)`OK`2`40`D NEXT`S ER=5,OK=1,SN=SN+1
 ;;AT+FET=*4`OK`1`85`D AGAIN`S ER=5,OK=1
 ;;ATZ`OK`2`2
 ;;
DATA2 ;;
 ;;ATHE1V1`OK`2`2
 ;;AT`OK`2`2`S ST=$H,(C,DTM,DCS,CSI)="" D RSEND
 ;;AT`OK`2`2`H 20
 ;;AT`OK`2`2`D FPOST,JOB S:JOB SN=SN-2
 ;;
DATA3 ;;
 ;;*3`OK`1`1``S (PTS,CSI)="" D DIAL S ST=$H D:OK EXEC
 ;;ATZ`OK`2`2
