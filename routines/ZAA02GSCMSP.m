ZAA02GSCMSP ;PG&A,ZAA02G-MTS,1.20,START PRINTER;6JAN95 11:30A;;;09JAN98  13:32
 ;Copyright (C) 1995, Patterson, Gray & Associates, Inc.
 ;
 ; PRNT entry point - enter with DOC,DIR,DV,CP (Y(x) optional)
 ;
PRNT S:'$D(PG) PG="ALL" S:'$D(CP) CP=1 S TDV=DV,Z=0
 S PG=$TR(PG,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
P0 L ZAA02GWP("CONTROL") F NQ=0:1 I $O(^ZAA02GWP(.9,TDV,NQ))'?1.N S NQ=NQ+1 Q
 D TIME S ^(NQ)="\"_DOC_"\"_CP_"\\"_PG_"\"_$S($D(BIN):BIN,1:"")_"\\"_TM_"\0\" L
 I $D(Y)>2 S B="" F I=1:1 S B=$O(Y(B)) Q:B=""  S ^ZAA02GWP(.9,TDV,NQ,B)=Y(B)
 I $D(COPY) S A="" F J=1:1 S A=$O(@DOC@(A)) Q:A=""  S ^ZAA02GWP(.9,TDV,NQ,"DOC",A)=^(A)
 D START K BIN,CP,PG,TM Q
 Q
 ;
QUEUE N (DV,CP,PG,DOC,Y,ZAA02GSCR) D PRNT Q
QUEUER N (DV,CP,PG,DOC,Y,ZAA02GSCR) S COPY=1 D PRNT Q  ; FOR REPORTS ONLY
 ;
TIME S B=$P($H,",",2),H=B\3600-1#12+1,M=B#3600\60 S TM=H_":"_$E("0"_M,$L(M),3)_" "_$S(B>43199:"PM",1:"AM") Q
 ;
START S VDV=TDV I $P($G(^ZAA02GWP(96,VDV)),"\",15)'="N",$G(^LPT(VDV))'["PASS" L +ZAA02GWP(VDV):0 Q:'$T  D SET J ^ZAA02GWPPC L -ZAA02GWP(VDV) Q
 D PASS
 Q
PASS N (ZAA02G,ZAA02GP,VDV) S (TDV,DP)=VDV W *27,"[5i" D Q1^ZAA02GWPPC U 0 W *27,"[4i" Q
 ;
SET S B=","_TDV_",",^ZAA02GWP(.9)=$S($D(^ZAA02GWP(.9))#2=0:B,^(.9)[B:B_$P(^(.9),B)_","_$P(^(.9),B,2,9),1:B_$E(^(.9),2,100)) Q
 ;
GETPRNT S %R=24,%C=1 W @ZAA02GP,ZAA02G("CL") S %C=30,X="" W @ZAA02GP,"Select Printer - " S %C=47 X ^ZAA02GREAD I X]"",'$D(^ZAA02GWP(96,X)) W "  NOT VALID PRINTER" H 2 G GETPRNT
 S DV=X S:DV=0 DV="" Q
 ;
DEFDV S:'$D(INP("DIST")) INP("DIST")=0 I $D(DISTR),DISTR]"",$D(@ZAA02GSCR@("PARAM","DIST",DISTR)) S INP("DIST")=DISTR
 S:'$D(P) P=0 S DV="" I P=1,$D(@ZAA02GSCR@("PARAM","DIST",INP("DIST"))) S Y=$O(^(INP("DIST"),"")) I Y S DV=$P(^(Y),"\",2) Q:DV]""
 I DV="",P=1,$D(@ZAA02GSCR@("PARAM","DEVICE")) X ^("DEVICE")
 S:P=0 P=1 I DV="",P<3,$D(TRANS) S DV=$P($P(@ZAA02GSCR@("PARAM","USER",TRANS),"\",10,11),"\",P)
 I DV="",P<3 S DV=$P($P(@ZAA02GSCR@("PARAM","BASIC"),"|",7,9),"|",P)
 Q
 ;
PR D DEFDV I DV="" D GETPRNT S INP("DIST")=""
 I $G(@ZAA02GSCR@("PARAM","PRINTQ"))]"" X ^("PRINTQ")
 Q
 ;
