ZAA02GSCRVA4 ;PG&A,ZAA02G-SCRIPT,2.10,VA TIFF DECODING;3JAN96 5:26P;;;25NOV96  16:26
 ;
DECODE ; DECODE TIFF
 W !!!,"Ready to RECEIVE graphic file via KERMIT",!!
 S MODEM=0,ERR=0,KP="",G=$H,GLOB="^ZAA02GKERMIT("""_G_""")",TGLOB="^ZAA02GKERMIT("""_G_""",1)",FGLOB=GLOB D INIT^ZAA02GSENDA,REXE^ZAA02GSENDE
 Q:$G(ERR)  W !!,"TRANSFER COMPLETED",!! X ^ZAA02G("ECHO-ON")
 R A:0,A:2 G X0
 ;
X0 K IFD S M=0,N=0,TR1=1,TR=$C(1,2),TR3=$C(2,1),$P(TR1," ",255)="" F J=3:3:252 S TR=TR_$C(J,J+1,J+2),TR3=$C(J+2,J+1,J)_TR3
 ; S G="56753,50773",(FGLOB,GLOB)="^ZAA02GKERMIT("""_G_""")",TGLOB="^ZAA02GKERMIT("""_G_""",1)"
 S G=TGLOB R "Store under what name? - ",NM,! K ^ZAA02GSCR("SIGN",NM),eS S:NM="eS" eS=1 W "wait..."
X K ADR,IDF  S J=$O(@G@("")),A=@G@(J),J=$O(@G@(J))
 ; 0 - 2  BYTE ORDER (MM-MAC OR II-INTEL)
 ; 2 - 2  VERSION # (ALWAYS 42)
 ; 4 - 4  POINTER TO FIRST IFD
 S BO=$E(A),IFDL=$$LWRD(A,5) I $E(A,3)'="*" W !,"NOT TIFF" Q
 ;
 D LOCATE(IFDL) S IFD=0,IFDC=$$WRD(A,1),A=$E(A,3,255),AD=AD+2 F L=1:1:IFDC D IFD
 D INITIFD
 I BITS'=1 W "NOT 1 BIT TIFF IMAGE" Q
 S NSTRIPS=LENGTH+ROWS-1\ROWS
 S L=STRIPS,STRIPS=IFD(L,"POINTER") I IFD(L,"TYPE")=4,IFD(L,"COUNT")=1 S STRIPS(1)=STRIPS
 E  D LOCATE($E(STRIPS,2,9)) F I=1:1:NSTRIPS S STRIPS(I)=$$LWRD(A,1),A=$E(A,5,255) I $L(A)<4 S A=A_@G@(J),J=$O(@G@(J))
 S WD=WIDTH/8,M=0,AD=-1 S:WD["." WD=WD\1+1 F STRIP=1:1:NSTRIPS S L=STRIPS(STRIP) D:AD'=L LOCATE(L) F M=M+1:1:M+ROWS Q:M>LENGTH  S S=$E(A,1,WD),A=$E(A,WD+1,255) D:$L(S)<WD DE1 S AD=AD+WD S:M#2 ^ZAA02GSCR("SIGN",NM,M)=$E(S,1,WD-1)
 G Z
DE1 S:$L(S)<WD A=@G@(J),J=$O(@G@(J)),K=$L(S),S=S_$E(A,1,WD-K),A=$E(A,WD-K+1,255) G:$L(S)<WD DE1
 Q
 ;
XX S G=$O(^ZAA02GKERMIT(999999)),TGLOB="^ZAA02GKERMIT("""_G_""",1)" G X0
 ;
Z S A=$D(^ZAA02GSCR("SIGN",NM,0))*0,L=256,R=0,T=0,B=0 F  S A=$O(^(A)) Q:A=""  S C=^(A) D TRIM
 S L=L-1,R=R-1 W:0 T," ",B," ",L," ",R,! I T S A="" F  S A=$O(^(A)) Q:A=T  K ^(A)
 S A=B F  S A=$O(^(A)) Q:A=""  K ^(A)
 S LM=R-L+1,HT=B-R,E=$G(^ZAA02GSCR("SIGN","eS")),H=LM*8-$P(E,",",2)\16,C=HT-E\4,E=0,H=$C(255-H,0)
 F  S A=$O(^ZAA02GSCR("SIGN",NM,A)) Q:A=""  S S=$E(^(A),L,R),X="" D DE0
 S ^ZAA02GSCR("SIGN",NM)=HT_","_(LM*8)
 R !!,"Signature Stored",!!,"Do you want to transfer another (Y or N) ",A,! G:A="Y" ZAA02GSCRVA4 Q
 ;
DE0 S S=$TR(S,TR,TR3),S=$TR(S,$C(0,255),$C(255,0))
DE2 S D=$TR(S,$C(255)_TR,TR1)_" ",D=$TR(D,$C(0)," "),I=$F(D," ") I I>3 S:I>129 X=X_$C(130,255),S=$E(S,128,255),I=I-127 S X=X_$C(259-I,255),S=$E(S,I-1,255) G DE3:S="",DE2
 S T=$F(D,111)
 S D=$TR(S,$C(0)_TR,TR1)_" ",D=$TR(D,$C(255)," "),I=$F(D," ") I I>3 S:I>129 X=X_$C(130,0),S=$E(S,128,255),I=I-127 S X=X_$C(259-I,0),S=$E(S,I-1,255) G DE3:S="",DE2
 S T1=$F(D,111) G:T+T1=0 DE4 S:T1&(T1<T)!(T=0) T=T1 S T1=$E(S,1,T-4),S=$E(S,T-3,255) S:$L(T1)>127 X=X_$C(127)_$E(T1,1,127),T1=$E(T1,128,255) S:$L(T1) X=X_$C($L(T1)-1)_T1 G DE2:S'="",DE3
DE4 S:$L(S)>127 X=X_$C(127)_$E(S,1,127),S=$E(S,128,255) S:$L(S) X=X_$C($L(S)-1)_S
DE3 S ^ZAA02GSCR("SIGN",NM,A)="*b2m"_$L(X)_"W"_X,C=C-1 Q:$D(eS)  S:$L(E) E=$O(^ZAA02GSCR("SIGN","eS",E)) S:C>0 E=0 S ^ZAA02GSCR("SIGN",NM,A+1)=$S(E:$P(^(E),"b2m")_"b2m"_($P(^(E),"b2m",2)+2)_"W"_H_$P(^(E),"W",2,255),1:"*b1Y"),A=A+1 Q
 ;
TRIM I 'T Q:$TR(C,$C(255))=""  S T=A
 S C=$TR($TR(C,$C(255)_TR,TR1),$C(0)," "),D=$F(C," ") I 'D S:'B B=A Q
 S B=0 S:D<L L=D F  Q:'$F(C," ",D)  S D=$F(C," ",D)
 S:D>R R=D Q
 Q
 ;
P ; TEST PRINTOUT
 S DV=13 O DV U DV I ^ZAA02G("OS")="MSM" U DV:(::::4096)
 S NM="" F  S NM=$O(^ZAA02GSCR("SIGN",NM)) Q:NM=""  I NM'="eS" W !!!,NM,!!,*27,"*t300R",*27,"*r1A" S A="" F  S A=$O(^ZAA02GSCR("SIGN",NM,A)) W:A="" *27,"*rB" Q:A=""  W *27,^(A)
 W # C DV Q
 ;
T(NM) N A W *27,"*t300R",*27,"*r1A" S A="" F  S A=$O(^ZAA02GSCR("SIGN",NM,A)) Q:A=""  W *27,^(A)
 W *27,"*rB" Q
TR(NM) W *27,"&f0S" D T(NM) W *27,"&f1S" Q
PR(NM) D T($P($G(^VA(200,NM,7)),"^",4)) Q
 ;
 ;
IFD ;  IFD DECODING
 ; 0 - 2 - COUNT
 ; 2 - 12 - ENTRY 0
 ; 14 - 12 - ENTRY 1
 ;   "
 ; n*12+4 - 4 POINTER TO SUBSEQUENT IFD IF ANY OR 0000
 ;
IFD1 S D=$E(A,1,12) I $L(D)<12 S A=A_@G@(J),J=$O(@G@(J)) G IFD1
 K XIFD S AD=AD+12,A=$E(A,13,255),IFD=IFD+1,(XIFD(IFD,"TAG"),IFD(IFD,"TAG"))=$$WRD(D,1),(XIFD(IFD,"TYPE"),IFD(IFD,"TYPE"))=$$WRD(D,3),(XIFD(IFD,"COUNT"),IFD(IFD,"COUNT"))=$$LWRD(D,5),(XIFD(IFD,"POINTER"),IFD(IFD,"POINTER"))=$$LWRD(D,9)
 X "N (XIFD,J,AD) W  R CCC"
 S W=$P("1,1,2,4,8,1,1,2,4,8,4,8",",",IFD(IFD,"TYPE")) I W*IFD(IFD,"COUNT")>4 S ADR(IFD(IFD,"POINTER"))=IFD,IFD(IFD,"POINTER")="&"_IFD(IFD,"POINTER")
 Q
INITIFD S BITS=1,COMPR=1,RPS=65536*65536-1,PIXELS=1
 F L=1:1:IFDC S T="T"_IFD(L,"TAG") D:$T(@T)'="" @T W:'$T(@T)="" T," "
 Q
LOCATE(X) S J=$O(@G@("")),A=@G@(J),J=$O(@G@(J)),L=0 F  Q:X<(L+$L(A))  S L=L+$L(A),A=@G@(J),J=$O(@G@(J))
 S AD=X,A=$E(A,X-L+1,255) Q
 ;
T254 S SUBFILE=IFD(L,"POINTER") Q
T256 S WIDTH=IFD(L,"POINTER") Q
T257 S LENGTH=IFD(L,"POINTER") Q
T258 S BITS=IFD(L,"POINTER") Q
T259 S COMPR=IFD(L,"POINTER") Q
T262 S MONO=IFD(L,"POINTER") Q
T273 S STRIPS=L Q
T277 S PIXELS=IFD(L,"POINTER") Q
T278 S ROWS=IFD(L,"POINTER") Q
T279 S STRIPB=IFD(L,"POINTER") Q
T282 S XRES=IFD(L,"POINTER") Q
T283 S YRES=IFD(L,"POINTER") Q
T284 S PLANAR=IFD(L,"POINTER") Q
T296 S RESU=IFD(L,"POINTER") Q
T317 S XXXX=IFD(L,"POINTER") Q
 ;
LWRD(S,x) I BO="I" Q $A(S,x+3)*256+$A(S,x+2)*256+$A(S,x+1)*256+$A(S,x)
 Q $A(S,x)*256+$A(S,x+1)*256+$A(S,x+2)*256+$A(S,x+3)
WRD(S,x) I BO="I" Q $A(S,x+1)*256+$A(S,x)
 Q $A(S,x)*256+$A(S,x+1)
