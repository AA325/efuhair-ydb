ZAA02GSCRVT ;PG&A;ZAA02G-SCRIPT;1.X;VOICE TECH INTERFACE;06OCT99  10:17
 ;
 L ^ZAA02GSCRVT:10 E  H
 D MODEM O MODEM::60 E  S ^ZAA02GSCRVTL("NOOPEN",$H)="" H
 U MODEM I ^ZAA02G("OS")="M11" U MODEM:(:"S")
 I ^ZAA02G("OS")="DTM" U MODEM:(IXXLATE=0:IXINTERP=0)
 S A=^ZAA02G("ERROR")_"=""ERROR^ZAA02GSCRVT""",@A,^ZAA02GSCRVTL("START",$H)="" K ^ZAA02GSCRVTL("STOP")
WAIT D ORD
 D REPT G WAIT
ORD S A="" F  G:$D(^ZAA02GSCRVTL("STOP")) HALT S A=$O(^ZAA02GSCRVT(A)) Q:A=""  D ORDB
 Q
ORDB Q:'$D(^ZAA02GSCRVT(A))  W $C(11),^ZAA02GSCRVT(A),$C(28) R R:30 I R'[$C(28) Q:$D(^ZAA02GSCRVTL("STOP"))  H 5 R B:25 G ORDB
 I R["RPT" D REPTI G ORDB
 I R["AR" S B=$P(R,"|",2),^ZAA02GSCRVTL("AR",B)=$G(^ZAA02GSCRVT(B)) K ^ZAA02GSCRVT(B) Q
 I R'["AA" G ORDB
 S B=$P(R,"|",2),^ZAA02GSCRVTL("AA",B)=$H_","_$G(^ZAA02GSCRVT(B)) K ^ZAA02GSCRVT(B)
 Q
 ;
REPTI N (MODEM) D REPT Q
REPT S I=1,COM=$G(^ZAA02GSCRVI)+1,^ZAA02GSCRVI=COM F  R A#200:30 G:'$T RTO I $L(A) S ^ZAA02GSCRVI(COM,I)=A,I=I+1 Q:A[$C(28)
 ; U 0 W "END",$L(A),!  U MODEM
 ; I A[$C(11) S ^ZAA02GSCRVI(COM,I-1)=$P(A,$C(11)),AA=$C(11)_$P(A,$C(11),2,99)
 I $G(^ZAA02GSCRVI(COM,1))[$C(11) S N=$P(^ZAA02GSCRVI(COM,1),"|",2) W *11,"ACK|",N,"|1.1|AA",*28,*13 J EXT^ZAA02GSCRVT Q
 S N=$P(^ZAA02GSCRVI(COM,1),"|",2) W *11,"ACK|",N,"|1.1|AR",*28,*13 Q
 Q
RTO Q:A=""&(I=1)  I I=1 G:A'[$C(11) REPT S N=$P($G(A),"|",2)
 I I>1 S N=$P($G(INPUT(COM,I)),"|",2)
 W *11,"ACK|",$G(N),"|1.1|AR",*28,*13 G REPT
 ;
DT(X) I X["*" S X=$P(X,"*")_10_$P(X,"*",2)
 S X=$P(X,"/",3)+1900*100+X*100+$P(X,"/",2) Q X
 ;
DTX(X) I 'X Q 19000101
 I X>18500000 Q X
 Q 19000000+X
 ; DATES YYYYMMDD
 ;
MODEM S MODEM=$S($D(^ZAA02GSCRVTL("MODEM")):^("MODEM"),1:"/dev/tty22") Q
ERROR S B="B="_^ZAA02G("ERRORR"),@B,^ZAA02GSCRVTL("ERROR",$H)=B H 5 Q
HALT S ^ZAA02GSCRVTL("STOPPED",$H)="" H
 ;
 ;                                       
FORMAT ;ORD|MESS ID|1.0|MR#|LAST|FIRST|DOB|SSN|ACCESS#|EXAMCODE|DOE|REFER
 ;ORD|MESS ID|1.1|MR#|LAST|FIRST|DOB|SSN|ACCESS#|EXAMCODE|DOE|RFCODE^RFF^RFL|RFA1|RFA2|RFCSZ|RFFAX
 ;RPT|MESSID|1.0|MR#|ACCESS#|PRIM PROV|SEC PROV|REPORT DATE|STATUS|TEXT|IMPRESSION TEXT
 ;   currently putting the MESS ID also in ACCESS# field
 ;
 ;INSERTED INTO ROUTINE ^APP3 :S ^MARKAP(^MARKAP)="" J POSTAP^ZAA02GSCRVT
 ;
POSTAP S A="" F  S A=$O(^MARKAP(A)) Q:A=""  S B=^(A) K ^(A) D POSTEP(B)
 Q
 ;
POSTEP(TR) ; ENTRY FROM APPOITG ROUTINE
 N (TR) S K=$P(TR,":",2),K1=$P(TR,":",3) Q:K=""
 S GRG=$G(^GRG(K)),PTG=$G(^PTG(K,K1)),MR=K
 S PNM=$P(PTG,":",3) S:PNM="" PNM=$P(GRG,":",7)
 S TT=$P(PTG,":",12),EXAM=$P(TR,":",7) S:EXAM="" EXAM="(NA)" S $P(GRG,":",19)=EXAM
 S RFG=$P($P($G(^APPG(+TR,K,K1)),":",4)," ") I RFG]"" S RFG=$G(^RFG(RFG))
 D RFF^ZAA02GADS1 S RFF=T D RFL^ZAA02GADS1 S RFL=T
 S RFA1=$P(RFG,":",4),RFA2=$P(RFG,":",5),RFCSZ=$P(RFG,":",6)_"  "_$P(RFG,":",7),RFFAX=$P(RFG,":",21)
 S TYPE="1.1" G QUEUE
 ;
 ;INSERTED INTO ROUTINE ^POS91 :S ^MARK(^TRG("Z"))=S J POSTE^ZAA02GSCRVT
 ;
POSTE S A="" F  S A=$O(^MARK(A)) Q:A=""  S B=^(A) K ^(A) D POST(B)
 Q
 ;
POST(TR) ; ENTRY FROM POSTING ROUTINE
 I $P(TR,":",2)'="" Q
 N (TR) S K=$P(TR,":",3),K1=1 Q:K=""  I K["/" S K1=$P(K,"/",2),K=+K
 S GRG=$G(^GRG(K)),PTG=$G(^PTG(K,K1)),MR=K ;,MR=$P(TR,":",8)
 S PNM=$P(PTG,":",3) S:PNM="" PNM=$P(GRG,":",7)
 S TT=$P(PTG,":",12),EXAM=$P(TR,":",6) S:EXAM]"" EXAM=$P($G(^INPC(EXAM)),":",2) S:EXAM="" EXAM="(NA)" S $P(GRG,":",19)=EXAM
 S RFG=$P(TR,":",14) S:RFG]"" RFG=$G(^RFG(RFG))
 D RFF^ZAA02GADS1 S RFF=T D RFL^ZAA02GADS1 S RFL=T
 S RFA1=$P(RFG,":",4),RFA2=$P(RFG,":",5),RFCSZ=$P(RFG,":",6)_"  "_$P(RFG,":",7),RFFAX=$P(RFG,":",21)
 S TYPE="1.1" G QUEUE
 ;
QUEUE S KK=K D DATE^ZAA02GSCRER
 S B="ORD||1.1|"_$S(K1>1:KK_"/"_K1,1:KK)_"|"_$P(PNM,",")_"|"_$S($E($P(PNM,",",2))=" ":$P(PNM,", ",2),1:$P(PNM,",",2))
 S B=B_"|"_$$DTX(TT)_"|123456789||"_$P($G(GRG),":",19)_"|"_$$DT(DT)_"|"_$P(RFG,":")_" "_$P($G(RFG),":",2)
 I $G(TYPE)="1.1" S $P(B,"|",3)=TYPE,$P(B,"|",13,16)=RFA1_"|"_RFA2_"|"_RFCSZ_"|"_RFFAX,$P(B,"|",12)=$P($P(B,"|",12)," ")_"^"_RFF_"^"_RFL,$P(B,"|",4)=MR
 S A=$G(^ZAA02GSCRVT) F A=A:-1:A-200 I $D(^ZAA02GSCRVTL("AA",A)),$P(B,"|",3,8)=$P(^(A),"|",3,8),$P(B,"|",10,11)=$P(^(A),"|",10,11) G Q1
 S (^ZAA02GSCRVT,A)=$G(^ZAA02GSCRVT)+1
Q1 S $P(B,"|",2)=A,$P(B,"|",9)=A,^ZAA02GSCRVT(A)=$TR(B,"/"_$C(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31)," ")
 I $D(^ZAA02GSCRVTL("STOP")) Q
 L +^ZAA02GSCRVT:0 I  L -^ZAA02GSCRVT J ^ZAA02GSCRVT
 Q
 ;
STATUS W !!!,"VOICE TECHNOLOGY INTERFACE STATUS",!!! I $O(^ZAA02GSCRVT(""))="" W "No Records in Queue",!!!
 E  W $G(^ZAA02GSCRVT)-$O(^ZAA02GSCRVT(""))+1," records in queue",!!!
 L ^ZAA02GSCRVT:0 E  W "Interface is currently RUNNING",!!!,"Do you want to stop it? (Y/N) " R A#1 S:A="Y"!(A="y") ^ZAA02GSCRVTL("STOP")=$H G STAT2
 L  D MODEM O MODEM::60 E  W "Interface is not running, but Port ",MODEM," is not available - cannot be started" H 5 G STAT2
 C MODEM W "Interface is not running - Do you want to start it now? (Y/N) " R A#1 I A="Y"!(A="y") J ^ZAA02GSCRVT W !!!,"process started" H 5 G STAT2
STAT2 Q
MENUSTAT D STATUS G MNUSP^MNU
 ;
EXT S A=^ZAA02G("ERROR")_"=""ERROR^ZAA02GSCRVT""",@A
EXTENTER I '$D(^ZAA02GSCR) S A="" F  S A=$O(^ZAA02GSCRVI(A)) Q:A=""  K ^ZAA02GSCRVI(A)
 Q:'$D(^ZAA02GSCR)
 D DATABASE^ZAA02GSCRPW,PARAM^ZAA02GSCRPW S A=""
EXT2 S A=$O(^ZAA02GSCRVI(A)) Q:A=""  G:$G(^(A)) EXT2 S ^(A)=1,H=^(A,1),L=$P(H,"|",10,99),F=$L(H)=200 S R=$P(H,"|",5) I R="" K:H[($C(11)_"ACKxx") ^ZAA02GSCRVI(A) G EXT2
 S R=$G(^ZAA02GSCRVTL("AA",R)) D EXT3
 S GLOB="^ZAA02GTMP($J)" K @GLOB S LN=1
 I $L(L)>65 D EXT1
 S B=1 F  S B=$O(^ZAA02GSCRVI(A,B)) Q:B=""  S C=^(B),F=$L(C)=200,L=L_$TR(C,"|"_$C(28)) D EXT1
 S @GLOB@(LN)=L,LN=LN+1,B=.03 I LN>1 F  S B=$O(@ZAA02GWPD@(B)) Q:B=""  I ^(B)["*" D EXTINS Q
 D EXT5
 L  S ^ZAA02GSCRVTL("REP",-DOC)=$H_","_A,^(-DOC,1)=H S ^ZAA02GSCRVI(A)=2 K:0 ^ZAA02GSCRVI(A) H 5 G EXT2
EXTINS S C=$O(^(B)) S:C="" C=B+100,^(C)=B_$C(1,1,1) S D=C-B\LN,J=$O(@GLOB@("")),$P(@ZAA02GWPD@(B),$C(1),4)=@GLOB@(J) F  S J=$O(@GLOB@(J)) Q:J=""  S @ZAA02GWPD@(B+D)=B_$C(1,1,1)_@GLOB@(J),B=B+D
 S $P(@ZAA02GWPD@(C),$C(1))=B Q
EXT1 F J=1:1:$L(L)\65 S C=$E(L,1,65),C=$P(C," ",1,$L(C," ")-1),L=$E($P(L,C,2,9),2,999) S @GLOB@(LN)=C,LN=LN+1
 I 'F S @GLOB@(LN)=L,L="",LN=LN+1
 Q
 ; mr,sitec,patient,dob,ds,template,proc1,consult
EXT3 N A,F,L D DATE^ZAA02GSCRER,TMPL^ZAA02GSCRER
 S (INP("TR"),TRANS)="VT",(OSET,OCOUNT,OT)="",(INP("DD"),INP("DT"))=DT,INP("TM")=TM,ZAA02G("RON")=$C(27)_"AAAA",XF=$C(1)
 S UP="ABCDEFGHIJKLMNOPQRSTUVWXYZ",LC="abcdefghijklmnopqrstuvwxyz",TIME=$P($H,",",2),STMC=0,ZAA02GWPD="@ZAA02GSCR@(DIR,DOC)",DIR="TRANS"
 S (REP,INP("TEMPLATE"))="blank",INP("SITEC")="O",INP("DIST")="A"
 ;
 S INP("MR")=$P(R,"|",4),INP("PATIENT")=$P(R,"|",5)_","_$P(R,"|",6),INP("DOB")=$$EXT4($P(R,"|",7)),INP("DS")=$$EXT4($P(R,"|",11))
 S INP("PROC1")=$P(R,"|",10),INP("PROC2")="",INP("CONSULT")=$P($P(R,"|",12)," "),X=$P($TR($P(H,"|",6),LC_",",UP_" ")," ") S A="" F  S A=$O(^PVG(A)) Q:A=""  I $E($P(^(A),":",2),1,$L(X))=X S X=A Q
 S INP("PROVIDER")=X
 I $D(@ZAA02GSCR@("PARAM","VT")) X ^("VT") ; site params here
 ; S X=INP("CONSULT") D FAX^ZAA02GSCRER2 ; CHECKS CONSULT
 ;
 D NEW^ZAA02GSCRER1 Q
 ;
EXT5 S ODOC=DOC N A,DOC S DOC=ODOC,P=4 S:0 INCOMP=1 D FILE^ZAA02GSCRER
 ; I ZAA02GSCRP["W" S STMC=STMC_",A" D MAMMO^ZAA02GSCMR K TYPS,EDIT
 S DV=5,INP("WORK")="",TRTYPE="" D STATS^ZAA02GSCRER2 D:P<3 PR2^ZAA02GSCRER2
 Q
 ;
EXT4(X) Q $E(X,5,6)_"/"_$E(X,7,8)_"/"_$E(X,3,4)
