ZAA02GSCRSRC ;PG&A,ZAA02G-SCRIPT,2.10,SOURCE COMPUTING INTERFACE;31AUG94 10:20A
 ;
LOAD ; LOADS INP ARRAY FOR ID WITH PATIENT INFO. GIVEN ACCOUNT #
 Q:'$D(^A(CLN,X))  S INP("PATIENT")=$P(^A(CLN,X,0),$C(127)),INP("DOB")=$E($P($G(^(3)),$C(127),2),5,6)_"/"_$E($P($G(^(3)),$C(127),2),7,8)_"/"_$E($P($G(^(3)),$C(127),2),3,4)
 ; S INP("DS")=$E($P($G(^(3)),$C(127),4),5,6)_"/"_$E($P($G(^(3)),$C(127),4),7,8)_"/"_$E($P($G(^(3)),$C(127),4),3,4)
 S:0 INP("PROVIDER")=$P(^(8),$C(127)) S INP("CONSULT")=$P(^(8),$C(127),3),INP("MR")=X
 ;
 S K=0 F J=1:1 S A=$P("MR,PATIENT,DOB,CONSULT,PROVIDER",",",J) Q:A=""  I $D(INP(A)) S K=K+1,opi(K)="PDSP;"_$P("MR,PATIENT,DOB,CONSULT,PROV",",",J)_";INP("""_A_""")"
 S opi=K,SRF=rf,rf=ZAA02G("HI") x:K op S rf=SRF K SRF,A
 I 1 Q
 Q
LOOKUP ; LOOKUP IN THE ^AN PATIENT FILE BY NAME
 I X["+" S X=$TR(X,"+") Q
 S X=$TR(X,"*"),PAT=X,PT=" "_$E($P(PAT,",",2)),PAT=$P(PAT,","),REFRESH=1 G:PAT="" LOOKE
 I $E($O(^AN(CLN,PAT)),1,$L(PAT))'=PAT G LOOKE
 I PT'="" S i=PAT F jj=1:1 S i=$O(^AN(CLN,i)) Q:i=""   Q:$E($O(^AN(CLN,i)),1,$L(PAT))=PAT&(" "_$P(i,",",2)[PT)  I $E($O(^AN(CLN,i)),1,$L(PAT))'=PAT G LOOKE
 D LOOK1 S tt=+REFRESH,td=$P(REFRESH,":",2) D AP0^ZAA02GFORM4
 I PAT[";EX" S X="" Q
 S X=$P(PAT,"|",2) D LOAD S X=INP("PATIENT")
LOOKE K i,jj,PAT Q
 ;
LOOK1 N:1 (ZAA02G,ZAA02GP,PAT,PT,CLN,REFRESH) S Y="15,7\RLHS\1\PATIENT LOOKUP\   NAME                  SEX    DOB      ACCNT #"
 S Y(0)="\EX\\$P(^A(CLN,S2,0),$C(127));25`$P($G(^(3)),$C(127));1`$E($P($G(^(3)),$C(127),2),5,6)_""/""_$E($P($G(^(3)),$C(127),2),7,8)_""/""_$E($P($G(^(3)),$C(127),2),3,4);10R`S2;8R\"_PAT_"\\\"_PAT_"z"
 S ZAA02GS1="S S=$P(TO,""|""),S2=$P(TO,""|"",2)"
 S ZAA02GPOPALT=$P($T(LOOKT),";",2,99),GLOBN="^AN"
 D ^ZAA02GPOP S PAT=X Q
 ;
LOOKT ;X ZAA02GS1 F jj=1:1 S:S2="""" S=$O(@GLOBN@(CLN,S)) S:jj=999 TO="""" S:S="""" TO="""" S:S]TEN TO=""""  Q:TO=""""  I "" ""_$P(S,"","",2)[PT S S2=$O(@GLOBN@(CLN,S,S2)) I $L(S2) S TO=S_""|""_S2 Q
 ;
CCC S FREE=1
REF ;LOOKUP REFERRAL BY NUMBER OR NAME - USED BY CC1,CC2 & REFERRAL
 S GLOBN="^ARDN",GLOB="^ARD",TITLE="REFERRAL FILE" G LOOKG
SITE ;LOOKUP SITE CODE BY NUMBER OR NAME - USED BY SITEC
 S GLOBN="^ALN",GLOB="^AL",TITLE="LOCATION CODES" G LOOKG
PROV ;LOOKUP PROVIDER BY NUMBER OR NAME - USED BY PROV
 S GLOBN="^AEN",GLOB="^AE",TITLE="PROVIDER FILE" G LOOKG
 ;
 ;
LOOKG S:X="" X="*" I X]"",$D(@GLOB@(CLN,X)) S:$D(FREE) X=$P($G(^(X,0)),$C(127)) G LOOKGE
 I X["+" S X=$TR(X,"+") G LOOKGE
 S X=$TR(X,"*"),PAT=$P(X,","),PATZ=PAT_"z",PT=" "_$P(X,",",2),REFRESH=1 I PAT="" S PAT="!",PATZ="zzzz" G LOOKG1
 I $D(@GLOBN@(CLN,PAT))=0,$E($O(@GLOBN@(CLN,PAT)),1,$L(PAT))'=PAT G LOOKGE
 I PT'="" S i=PAT F jj=1:1 S i=$O(@GLOBN@(CLN,i)) Q:i=""   Q:$E($O(@GLOBN@(CLN,i)),1,$L(PAT))=PAT&(" "_$P(i,",",2)[PT)  I $E($O(@GLOBN@(CLN,i)),1,$L(PAT))'=PAT G LOOKGE
LOOKG1 D LOOKR S tt=+REFRESH,td=$P(REFRESH,":",2) D AP0^ZAA02GFORM4
 I PAT[";EX" S X="" S:$D(FREE) X=x0 Q
 S X=$P(PAT,"|",'$D(FREE)+1) I 1
LOOKGE K TITLE,GLOB,GLOBN,i,jj,PAT,PT,PATZ,FREE Q
 ;
LOOKR N:1 (ZAA02G,ZAA02GP,PAT,PATZ,PT,CLN,REFRESH,GLOB,GLOBN,TITLE) S Y="15,7\RLHS\1\"_TITLE_"\CODE         NAME          "
 S Y(0)="\EX\\S2;6`$P(@GLOB@(CLN,S2,0),$C(127));20\"_PAT_"\\\"_PATZ
 S ZAA02GS1="S S=$P(TO,""|""),S2=$P(TO,""|"",2)"
 S ZAA02GPOPALT=$P($T(LOOKT),";",2,99)
 D ^ZAA02GPOP S PAT=X Q
 ;
REFRESH G AP0^ZAA02GFORM4
 ;
FAXS ; AUTOFAX CODE HERE
 Q
 ;
VAR ; LOOKUP FOR DOCUMENT ~$ PARAMETERS
 F I=1:1 S VAR=$P(ALLVAR,",",I) Q:VAR=""  D VAR1
 Q
VAR1 Q:VAR=""  Q:$T(@VAR)=""  D @VAR S INP(VAR)=T
NOTES S T="NOTES FROM ZAA02GSCRMID" Q
PV ;
DR S T=INP("PROVIDER") Q:T=""  S T=$P($G(^AE(CLN,T,0)),$C(127),6) Q
PVI S T=INP("PROVIDER") Q:T=""  S T=$P($G(^AE(CLN,T,0)),$C(127),2) Q
LOC S T=INP("SITEC") Q:T=""  S T=$P($G(^AL(CLN,T,0)),$C(127)) Q
DS S T=INP("PROC2") Q
PN S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,0)),$C(127)) S T=$P(T,",",2)_" "_$P(T,",",1)_" " S:$E(T,1)=" " T=$E(T,2,$L(T)) S:T?." " T=$G(INP("PATIENT")) Q
PFN S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,0)),$C(127)) S T=$P(T,",",2) S:$E(T,1)=" " T=$E(T,2,$L(T)) Q
PLN S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,0)),$C(127)) S T=$P(T,",") Q
PA1 S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,0)),$C(127),2)
PA2 S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,0)),$C(127),3)
PCSZ S T=INP("MR") Q:T=""  S V9=$P($G(^A(CLN,T,0)),$C(127),4),DEL=$C(127) D ^ZIPL S T=V9 Q
DOI S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,3)),$C(127),4) Q
SSN S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,9)),$C(127),3) Q
SEX S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,3)),$C(127),1) Q
AGE S T=INP("MR") Q:T=""  S T=$P($G(^A(CLN,T,3)),$C(127),1) Q
RFN S T=INP("CONSULT") Q:T=""  S T=$P($G(^ARD(CLN,T,0)),$C(127),2) Q
RFF S T=INP("CONSULT") Q:T=""  S T=$P($P($G(^ARD(CLN,T,0)),$C(127),1),",",2) S:$E(T)=" " T=$E(T,2,99) Q
RFL S T=INP("CONSULT") Q:T=""  S T=$P($P($G(^ARD(CLN,T,0)),$C(127),1),",") Q
RFA1 S T=INP("CONSULT") Q:T=""  S T=$P($G(^ARD(CLN,T,2)),$C(127),1) Q
RFA2 S T=INP("CONSULT") Q:T=""  S T=$P($G(^ARD(CLN,T,2)),$C(127),2) Q
RFCSZ S T=INP("CONSULT") Q:T=""  S V9=$P($G(^ARD(CLN,T,2)),$C(127),3),DEL=$C(127) D ^ZIPL S T=V9 Q
 ;
CC1N S T=INP("CC1") Q:T=""  S T="CC:  "_T Q
CC2N S T=INP("CC2") Q:T=""  S T="     "_T Q
CC3N S T=INP("CC3") Q:T=""  S T="     "_T Q
