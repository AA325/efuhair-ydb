ZAA02GSCRER ;PG&A,ZAA02G-SCRIPT,2.10,ADD/EDIT REPORT ;18NOV94  01:18;;;Mon, 07 DeC 2009  08:39
 ;Copyright (C) 1992, Patterson, Gray & Associates, Inc.
INIT D HEAD1,DATE,TMPL
 K INP
 S INP("TR")=TRANS,(OSET,OCOUNT,OT)="",(INP("DD"),INP("DT"))=DT,INP("TM")=TM,INP("WORK")=WORKT,$P(INP("DD"),"/",2)="*"
 I $D(INPS) S I="" F J=1:1 S I=$O(INPS(I)) Q:I=""  S INP(I)=INPS(I)
I1 S SCR="ZAA02GSCRIPT",SNL=$P(ZAA02GSCRP,";",2),REFRESH="3:24",ZAA02Gform="0;;;N",UP="ABCDEFGHIJKLMNOPQRSTUVWXYZ",LC="abcdefghijklmnopqrstuvwxyz",%R=3,TIME=$P($H,",",2),STMC=0 W @ZAA02GP,ZAA02G("CS")
 I OSET]"" G:$TR(ZAA02GSCRP,"is","??")["?" NOMOD^ZAA02GSCRER3
I3 K ESREMOVE I TRTYPE>0,$E($G(INP("eS")))="Y",$P(@ZAA02GSCR@(DIR,DOC),"`",13)["E" D LOCKE^ZAA02GSCRER3
 S:TRTYPE<1 ZAA02Gform="1;N;;",INQ="INQ"
 S formHELP="SHELP^ZAA02GSCRHP;D" D ^ZAA02GFORM I ZAA02Gform["TIME" G AGAIN:$D(ED),END
I2 S ZAA02GWPD="@ZAA02GSCR@(DIR,DOC)",DIR="TRANS",REP=INP("TEMPLATE")
 D TOP,NEW^ZAA02GSCRER1,EDIT:$G(edit)<9 S ZAA02GWPD="@ZAA02GSCR@(DIR,DOC)" K ^ZAA02GSCR("LOCK",+$H,DOC) I $D(LOCK) S P=LOCK D FILE G AGAIN
MNU I ZAA02GSCRP["W" S STMC=STMC_",A" D MAMMO^ZAA02GSCMR K TYPS,EDIT
 S A=+OCOUNT=0!(ZAA02GSCRP'["D")&TRTYPE&($G(INQ)'="INQ")
 S %R=24,%C=1 W @ZAA02GP,ZAA02G("CS") S Y="10,24\QHP1U\*\\\Print,Alternate Print,Edit,Hold,Review"_$S(A:",Delete",1:"")_",Quit"
 D ^ZAA02GPOP K Y W *13 S P=$S(X["EX":7,1:X)
 I P=6 G DELETE:A
 S:ZAA02GSCRP["O" INP("TR")=TRANS
 D FILE
 S TIME=$P($H,",",2)-TIME S:TIME<0 TIME=TIME+86400 S TIME=TIME+3\6
 I 34567[P S $P(@ZAA02GWPD@(.011,"STATS"),",",7,8)=$P(OCOUNT,",",7)+TIME_","_($P(OCOUNT,",",8)+STMC)
 I P=3 S OSET=@ZAA02GWPD,OT=INP("TEMPLATE") D HEAD,HEAD1 G I1
 I 12[P D PRS^ZAA02GSCRER2 X:$D(EXE) EXE
 G AGAIN
DELETE D DELETE^ZAA02GSCRER2 I A G AGAIN
 G MNU
AGAIN L  K LOCK,INQ,edit D HEAD G INIT:'$D(ED),@$P("ED,RV,ALL,MAMMO,UL,INC,HOLD",",",ED)
ED S ED=1 G EDD
RV S ED=2 G EDD
ALL S ED=3 G EDD
MAMMO S ED=4 G EDD
UL S ED=5 G EDD
INC S ED=6 G EDD
HOLD S ED=7
EDD S DOC="T R A N S C R I P T   E D I T,T R A N S C R I P T   R E V I E W,M A S T E R   F I L E   E D I T,M A M M O G R A P H Y   R E P O R T S,"
 S DOC=DOC_"U P L O A D E D   R E P O R T S,I N C O M P L E T E  R E P O R T S,R E P O R T S  O N  H O L D"
 S %R=1,%C=20,DOC=$P(DOC,",",ED)
 W @ZAA02GP,ZAA02G("HI"),$J("",42-$L(DOC)\2),DOC,$J("",44-$L(DOC)\2),!!,ZAA02G("CS")
 K INP D DATE D @($P("TR,RV,ALL,MAMMO,UL,INC,HOLD",",",ED)_"^ZAA02GSCRTR") G:DOC="" END D FETCH G I1
END K OCOUNT,DV,DT,DCOUNT,ZAA02GWPD,ED,DIR,YM,TM,AST,HLPR,UP,LC,L,S,B,C,W,TY,TIME,P,OT Q
FETCH S DIR="TRANS"
F1 S OSET=@ZAA02GSCR@(DIR,DOC),Y=^(DOC,.011) D TMPL F J=1:1:$L(TMPL,",") S INP($P(TMPL,",",J))=$P(Y,"`",J)
 S OCOUNT=$S($D(^(.011,"STATS")):^("STATS"),1:""),OT=INP("TEMPLATE") I OCOUNT="",$G(DT)]"",DT'=INP("DT"),'$D(REBUILD) S INP("DT")=DT
 I $O(^(0))'="" S B="" F J=1:1 S B=$O(^(B)) Q:B=""  S:'$D(INP(B)) INP(B)=$G(^(B))
 F B="STATS","IO","MAMMO","LOCKED" K INP(B)
 Q
TMPL S TMPL="PROC1,PROC2,TEMPLATE,TEMPLATE2,TR,SITEC,PROVIDER,PATIENT,MR,DOB,DD,DT,TM,REV,DIST,CONSULT,CC1,CC2,WORK,P1,P2" Q
 ;
PRR G PR^ZAA02GSCRER2
FILE S Y=$P(@ZAA02GWPD,"`",13),X="" I Y["H",P'=4 D:'$D(P1) HOLDM S X=$S(X=2:"H",1:"") K:X="" @ZAA02GSCR@("DIR",12,"H",10000000-DOC)
 S Y=$E($TR("HEIPR",$TR("HEIPR",$TR(Y,"HRIN")_$E("   HR  ",P)))_$S($D(INCOMP):"I",1:"")_$S($G(INP("NOTE"))]"":"N",1:"")_X_"~   ",1,4)
 I P=4!$D(INCOMP)!(X="H") S:P=4!(X="H") @ZAA02GSCR@("DIR",12,"H",10000000-DOC)=TRANS S:$D(INCOMP) @ZAA02GSCR@("DIR",12,"I",10000000-DOC)="",@ZAA02GWPD@(.011,"INCOMP")=$G(INCOMP) K INCOMP
 S:P=5 @ZAA02GSCR@("DIR",12,"R",10000000-DOC)=""
 S $P(@ZAA02GWPD,"`",13)=Y,$P(@ZAA02GSCR@("DIR",99,10000000-DOC),"`",13)=Y
REV S X=$G(@ZAA02GWPD@(.011,"LE"))+1,$P(^("LE"),";",1)=X,$P(^("LE"),";",X#10+2)=$TR($H,",",":")_","_$G(USER,TRANS)
 I $D(^ZAA02GACT) S ^ZAA02GACT(INP("MR"),"TRANS",DOC)=$H_":"
 Q
TOP S %R=1,%C=2,A="" W ZAA02G("F"),@ZAA02GP,ZAA02G("LO"),"Patient: ",ZAA02G("HI"),INP("PATIENT") W:$L(INP("PATIENT"))<35 "  (",INP("MR"),$S($G(INP("AGE"))]"":" "_INP("AGE"),1:""),")"
 I $P(ZAA02GSCRP,";",4)=1,$G(INP("CONSULT"))]"" S X=INP("CONSULT") D FAXINFO^ZAA02GSCRFX I FAXTO]"" S A="Referral: "_FAXTO S:FON]"" A=A_" ("_FON_")"
 I $P(ZAA02GSCRP,";",4)=2 S A="Provider: "_INP("PROVIDER")
 I A]"" S %C=80-$L(A) W @ZAA02GP,ZAA02G("LO"),$P(A,":"),":",ZAA02G("HI"),$P(A,":",2,9)
 Q
EDIT G:$P(TRTYPE,"\",6)="Y" WP^ZAA02GSCRER3 I ZAA02GSCRP["e" X $G(@ZAA02GSCR@("PARAM","XEDIT")) Q:$G(LOCK)  D EDIT1 X $G(@ZAA02GSCR@("PARAM","XEDIT1")) Q
EDIT1 D:$D(ad) CL^ZAA02GSCRER3
 N DT,ED S TL=3,BL=22,WO="",MG=$S($D(@ZAA02GWPD@(.01)):^(.01),1:76),NM=DOC,ZAA02GWPOPT="NC,AT,"_$S($P(TRTYPE,"\",4)="Y":"IM,",1:"")_"OT1,2,3,4,5,6,8,9,10,14,15,16,18"_$G(INQ),AST="*",HLPR="^ZAA02GSCRH",W=1
 K GS S:'$D(GTF) GTF=$P(TRTYPE,"\",3) I $P(TRTYPE,"\",5)="Y",^ZAA02G("OS")'="DTM" S GS="ADHJLNO",GF=",,",(GSF,GSFO)=""
 D ENTRY S ZAA02G("MENU")=0 X:$D(EXED) EXED D:$D(ad) RIAD^ZAA02GSCRER3  Q
 ;
ENTRY I $D(@ZAA02GWPD@(.0115,5)) S B="" D ZAA02GGETTX^ZAA02GVBTER1  S ZAA02GWPOPT=ZAA02GWPOPT_",INQ"
 D ENTRY^ZAA02GWPV6 Q
DATE S K=$S(1:$H,$P($H,",",2)<18000:$H-1,1:$H) D D1 S YM=$E(Y,2,5),YM=$S(YM<3000:200000,1:190000)+YM,DY=D I $P($H,",",2)<18000 S K=$H D D1
 S TM=$P($H,",",2)\60,TM=$E(TM\60+100,2,3)_":"_$E(TM#60+100,2,3) K K,Y,M,D Q
D1 S K=K>21608+305+K,Y=4*K+3\1461,D=K*4+3-(1461*Y)+4\4,M=5*D-3\153,D=5*D-3-(153*M)+5\5,M=M+2,Y=M\12+Y+40,M=M#12+1,Y=Y*100+M*100+D,DT=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3) Q
 ;
HEAD W ZAA02G("F") S %R=1,%C=2 I $D(ZAA02G("a"))=0 W ZAA02G("LO"),@ZAA02GP,ZAA02G("RON")," P ",ZAA02G("RT")," G ",ZAA02G("RT")," & ",ZAA02G("RT")," A " S %C=67 W @ZAA02GP,"  ZAA02G-SCRIPT  ",ZAA02G("ROF"),!
 E  S A=$P(ZAA02G("a"),"`"),I=$P(ZAA02G("a"),"`",2),%C=18 W @ZAA02GP,I,*13," ",ZAA02G("HI"),A,"P ",I,A,"G ",I,A,"& ",I,A,"A ",I S %C=68 W @ZAA02GP,A,"ZAA02G-SCRIPT ",I,!
 W ZAA02G("G1") S J=ZAA02G("HL"),J=J_J_J_J F I=1:1:20 W J
 W ZAA02G("G0") Q
 ;
HEAD1 S %R=1,%C=20 W @ZAA02GP,ZAA02G("HI")," T R A N S C R I P T   D E F I N I T I O N   ",!!,ZAA02G("CS") Q
 ;
HOLDM N (ZAA02G,ZAA02GP,X) S Y="10,24\QUHP3\*\\\HOLD STATUS:*,Keep on Hold,Remove from Hold" D ^ZAA02GPOP
 Q
