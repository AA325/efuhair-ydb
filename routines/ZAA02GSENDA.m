ZAA02GSENDA ;PG&A,ZAA02G-SEND,1.36,MODEM TRANSFER (Kermit-2);11APR96 2:41P;;;05SEP96  10:43
 ;PG&A, 1993
KERM S %R=1,%C=24 W @ZAA02GP,"   K E R M I T   T R A N S F E R S   " S %C=1,%R=3 W @ZAA02GP,ZAA02G("CS")
K1 W !,ZAA02G("LO"),"KERMIT:  ",ZAA02G("HI"),"S",ZAA02G("LO"),"end, ",ZAA02G("HI"),"R",ZAA02G("LO"),"eceive, ",ZAA02G("HI"),"G",ZAA02G("LO"),"et, or ",ZAA02G("HI"),"M",ZAA02G("LO"),"enu ",ZAA02G("HI")
 R A#1 S:A?1L A=$C($A(A)-32) I "SRMG"'[A W *7,*13 G K1
 Q:A=""  W !! G ^ZAA02GSENDC:A="M" D INTER^ZAA02GSEND8 S:S=2 A="R" S DIRECT=$S(A="S":"L",1:"R"),B="" W ! G:DIRECT="R" RKERMITE
K2 W ZAA02G("LO"),"SEND:",ZAA02G("HI")," G",ZAA02G("LO"),"lobals, ",ZAA02G("HI"),"R",ZAA02G("LO"),"outines, or ",ZAA02G("HI"),"F",ZAA02G("LO"),"iles ",ZAA02G("HI")
 R B#1 S:B?1L B=$C($A(B)-32) I "GRF"'[B W *7,*13 G K2
 G K1:B="" S TYPE=$S(B="F":"FILE",B="G":"GLO",1:"ROU")
 W ! I B="F" U 0 R "SEND HOST FILE?-",B,! Q:B=""  I B'[";" W !,"REMOTE FILE (",B,")-" R GL S B=B_";"_$S(GL="":B,1:GL)
 I B="G" R "GLOBAL?-",B,! Q:B=""  S:$E(B)'="^" B="^"_B R "Send Global References? (Y or N)-",A,! S A=$E(A) S:"Yy"[A $P(B,";",4)=2
 I B="R" X LRSEL Q:$O(^UTILITY(%J,0))=""  S B="^UTILITY("_%J_")"
 S B=B_";;^ZAA02GKERMIT("_(+$H)_","_$P($H,",",2)_")"
 ;
SKERMIT ;SKERMIT-GLOBAL/ROUTINE/FILE;REMOTEFILE;LOG;SFL
 N T,D S:'$D(TYPE) TYPE="GLO"
 D SETSCR K GL S GLOB=$S($P(B,";",3)="":"^ZAA02GSEND(""XFR"")",1:$P(B,";",3)),@GLOB@(0,"FILE",1,0)=$P(B,";"),@GLOB@(0,"FILE",1)=$S($P(B,";",2)]"":$P(B,";",2),1:"ZAA02G"_TYPE),SFL=$P(B,";",4)
 S:SFL="" SFL=1 D @("SEND"_TYPE_"^ZAA02GSENDD") K GL Q
RKERMITE W ! I A="G" U 0 R "GET WHICH REMOTE FILE?-",A,! Q:A=""  S B="",$P(B,";",4)=A
RKERMIT ;RKERMIT-GLOBAL;;LOG
 I $P(B,";",4)]"" S SD=$P(B,";",4),SvB=B D INIT S SP="R" D SEND^ZAA02GSENDE S B=SvB K SvB
 N D S:$P(B,";")="" $P(B,";")="^ZAA02GKERMIT("_(+$H)_","_$P($H,",",2)_")" D SETSCR S FGLOB=$P(B,";"),GLOB=$P(B,";",3) S:GLOB="" GLOB=FGLOB D INIT,REXE^ZAA02GSENDE Q
SETSCR S (DV,ER)=0 X LECHOF S A=^ZAA02G("ERROR")_"=""TRAP^ZAA02GSENDA""",@A Q
 ;         
TRAP U 0 W "PROGRAM ERROR - ",@^ZAA02G("ERRORR"),! H 3 Q
RPARM S RPARM=RD,RD=RD_$E("~  @_#N",$L(RD)+1,20),RT=$A(RD,2)-32,SA=$A(RD,3)-32,SC=$A(RD,4),SC=$S(SC\64#2:SC-64,1:SC+64),SE=$A(RD,5)-32,RQ=$E(RD,6),SW=0,SE=$S(SE=0:"",1:$C(SE))
 S R8=$E(RD,7),RK=$E(RD,9),RS=$A(RD,10)-32,S8=$S(R8_S8?2.P:R8,R8_S8?1"Y"1P:S8,1:"N"),SK=$S(RK_SK?2.P:RK,RK_SK?1"Y"1P:SK,1:"N")
 S:RS\4#2 SW=$A(RD,11)-32 S:SW>RW SW=RW I RS\2#2 S A=$A(RD,12)-32*95+$A(RD,13)-32 S:A<SZ SZ=A
 I $L(RD)<9 S SZ=$A(RD)-32,SW=0
 S X=$C($S(RDZ>94:80,1:RDZ)+32) S SD=X_$C(ST#94+32,RA#94+32,RC#64+64,RE#64+32)_SQ_S8_"1"_SK_$C(32+(SW>0*4)+(RDZ>94*2)+8)_$C(SW+32,RDZ\95+32,RDZ#95+32) S @GLOB@(0,FILE,"RPARM-RD")=RD,@GLOB@(0,FILE,"RPARM-SD")=SD
 D FIXPARM I KP["D" U 0 S %R=10,%C=33 W @ZAA02GP,RDZ
 Q
FIXPARM S RK=$TR(RK,"YN ",""),S8=$TR(S8,"YN ",""),SK=$TR(SK,"YN ",""),R8=$TR(R8,"YN ",""),SE=$C(13),S9=$C($A(SQ)+128) Q
 ;
SPARM S SD=$C($S(RDZ>94:80+32,1:RDZ),RT#94+32,SA#94+32,SC#64+64,SE#94+32)_SQ_$E(S8_" ")_"1"_$E(SK_" ")_$C(32+(SW>0*4)+(RDZ>94*2)+8)_$C(SW+32,RDZ\95+32,RDZ#95+32) S @GLOB@(0,"SPARM-SD")=SD Q
SPARM1 S @GLOB@(0,"SPARM-RD")=RD,RD=RD_$E("~  @_#N         ",$L(RD)+1,20),RZ=$A(RD)-32,SA=$A(RD,3)-32,SC=$A(RD,4),SC=$S(SC\64#2:SC-64,1:SC+64),SE=$A(RD,5)-32,SQ=$E(RD,6),RW=SW,SW=0,SE=$S(SE=0:"",1:$C(SE))
 S R8=$E(RD,7),SK=$E(RD,9),SS=$A(RD,10)-32,S8=$S(S8?1P&(R8=S8!(R8="Y")):S8,1:"") I SS S:SS\4#2 SW=$A(RD,11)-32 S:SW=1 SW=0 S:SW>RW SW=RW I SS\2#2 S A=$A(RD,12)-32*95+$A(RD,13)-32 S:A RZ=A
 S SZ=$S(RZ>SZ:SZ,1:RZ),SIZE=SZ-$S(SZ>94:8,1:5)
 I KP["D" U 0 S %R=10,%C=33 W @ZAA02GP,SZ
 D FIXPARM I 0
 Q
TESTP S MODEM=5,GLOB="GLO",FILE="FILE",KP="" D INIT,SPARM W "LOCAL SD: ",SD,! S RD=SD D RPARM W "REMOT SC: ",SD,! S RD=SD D SPARM1 W "RK=",RK Q
DISP S RP=" ",VL=ZAA02G("VL"),HL=ZAA02G("HL"),LIN="",$P(LIN,HL,35-2)=""
 S %R=3,%C=1 W @ZAA02GP,ZAA02G("CS") S %R=6,%C=23 W @ZAA02GP,ZAA02G("LO"),ZAA02G("G1"),ZAA02G("TLC"),LIN,ZAA02G("TRC")
 S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0")," ZAA02G-SEND                 KERMIT ",ZAA02G("G1"),VL
 S %R=%R+1 W @ZAA02GP,ZAA02G("LI"),LIN,ZAA02G("RI") S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0"),ZAA02G("LO"),"   Mode: ",ZAA02G("HI"),$P("RECEIVE,SEND   ",",",$E(PTR)="S"+1),"                ",ZAA02G("LO"),ZAA02G("G1"),VL
 S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0"),ZAA02G("LO")," P-Size: ",ZAA02G("HI"),$E($S($E(PTR)="S":SZ,1:RDZ)_"           ",1,10),"             ",ZAA02G("LO"),ZAA02G("G1"),VL
 S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0"),ZAA02G("LO"),"   File: ",ZAA02G("HI"),$E($G(FILEN)_"          ",1,10),"             ",ZAA02G("LO"),ZAA02G("G1"),VL S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0"),ZAA02G("LO"),"  Bytes:             file/total ",ZAA02G("LO"),ZAA02G("G1"),VL
 S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0"),ZAA02G("LO"),"   Rate:             char/sec   ",ZAA02G("LO"),ZAA02G("G1"),VL
 S %R=%R+1 W @ZAA02GP,ZAA02G("LI"),LIN,ZAA02G("RI") S %R=%R+1 W @ZAA02GP,VL,ZAA02G("G0")," Packet:     Type:    Errors:   ",ZAA02G("G1"),VL
 S %R=%R+1 W @ZAA02GP,ZAA02G("BLC"),LIN,ZAA02G("BRC"),ZAA02G("G0"),ZAA02G("HI") K LIN,VL Q
 ;
INIT D:$D(ZAA02G)<11 INIT^ZAA02GDEV I '$D(LECHOF) S LOS=^ZAA02G("OS") D OSS^ZAA02GSEND2
 S DV=MODEM O DV X LECHOFN W *17 U 0 I '$D(ZAA02GSENDG) S ZAA02GSENDG=$S($D(^%ZAA02GSEND)#2:^%ZAA02GSEND,1:"^ZAA02GSEND")
 I LOS="DTM",MODEM>1 U MODEM:IXXLATE=0
 K END,WO S SZ=240,ST=60,SH=1,SE=13,SQ="#",SW=8,(SPS,SA,SN,SEQ)=0,SC=64,WO=0,SK="~",S8="Y"
 S RDZ=240,RT=15,RH=1,(RN,LN,RA)=0,RK="~",RC=64,RE=13,RW=8,NERR=10
 I $D(ZAA02GSENDG),$D(@ZAA02GSENDG@("KERMIT")) S A=^("KERMIT") F J=1:1:17 S B=$P("SH,SE,SZ,ST,RH,RDZ,RT,NERR,ACZ,TRC,ATR,SQ,S8,SK,SA,SC,SW",",",J) S @B=$P(A,"\",J)
 S SH=$C(SH),RH=$C(RH) S:'$D(SDATA) SDATA="S SDB=$O(@GLOB@(FILE,SDB)) S:SDB SD=^(SDB)" S:SZ<30 SZ=30 S:RDZ<30 RDZ=30
 S CRC=$S(^ZAA02G("OS")="MSM":"S CS=CS+$ZCRC(X,1)",^("OS")="PSM":"S CS=CS+$ZX(X,2)",^("OS")="DTM":"S CS=CS+$ZCRC(X,0)",1:"F I=1:1:$L(X) S CS=CS+$A(X,I)")
 S (TT,CT,SP)=0,(TBY,SBY)=0,SE=$C(SE),RE=$C(RE),KP=$S($G(TRC)="Y":"T",$D(TRACE)=0:"D",TRACE:"T",1:"D") Q
 ;
