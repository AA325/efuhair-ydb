ZAA02GSCRVT1 ;PG&A;2014-07-10 17:23:46;1.X;VOICE TECH INTERFACE - TCP;14DEC98  16:58
 ;
 J INPUT^ZAA02GSCRVT1,OUTPUT^ZAA02GSCRVT1
 Q
START K ^ZAA02GSCRVTL("STOP") D ^ZAA02GSCRVT1 W "Interface Started" H 3 G ^MNU
STOP S ^ZAA02GSCRVTL("STOP")=$H W "Interface Coming Down" H 3 G ^MNU
 ;
OUTPUT ;  HERE TO START CLIENT TO SEND ORDERS
 H 5 K  L ^ZAA02GSCRVT:10 E  H    ; H 5 TO LET INPUT START
 S A=^ZAA02G("ERROR")_"=""ERROR^ZAA02GSCRVT1""",@A,^ZAA02GSCRVTL("START",$H,1)="O" K ^ZAA02GSCRVTL("STOP")
 D IP I IP="" S ^ZAA02GSCRVTL("NOOPEN",$H)="NO ADDRESS DEFINED" H
 S port=PORTO D TCPC I TCP="" S ^ZAA02GSCRVTL("NOOPEN",$H)="Open Error "_port_"-"_$G(lzb)_":"_$G(lzc) Q
WAIT D ORD H 30 G WAIT
ORD S A="" F  G:$D(^ZAA02GSCRVTL("STOP")) HALT S A=$O(^ZAA02GSCRVT(A)) Q:A=""  U TCP D ORDB
 Q
ORDB Q:'$D(^ZAA02GSCRVT(A))  W $C(11),^ZAA02GSCRVT(A,1),*13 F J=2:1 Q:'$D(^(J))  W ^(J),*13
 W $C(28,13),! R *R:30 I R'=11 Q:$D(^ZAA02GSCRVTL("STOP"))  H 5 R *B:25 G ORDB:B'=11
 S R="" F  R *B:25 G:'$T ORDB Q:B=28  S R=R_$S($L(R)<255:$C(B),1:"")
 G:R'["MSA|A" ORDB  S B=$P($P($P(R,"MSA|",2),"|",2),$C(13)) G:B="" ORDB
 I R["MSA|AR"!(R["MSA|AE") S ^ZAA02GSCRVTL("AR",B)=$G(^ZAA02GSCRVT(B))_";"_R K ^ZAA02GSCRVT(B) Q
 G:R'["AA" ORDB S ^ZAA02GSCRVTL("AA",B)=$H_","_$G(^ZAA02GSCRVT(B)) M ^ZAA02GSCRVTL("SENT_ORDERS",-B)=^ZAA02GSCRVT(B) K ^ZAA02GSCRVT(B)
 Q
INPUT ; HERE TO START SERVER TO RECEIVE REPORTS
SREP K  L ^ZAA02GSCRVTR:10 E  H
 S A=^ZAA02G("ERROR")_"=""ERROR^ZAA02GSCRVT1""",@A,^ZAA02GSCRVTL("START",$H,2)="I"
 D IP
WAITG S port=PORTI D TCPS I TCP="" S ^ZAA02GSCRVTL("NOOPEN",$H)="Open Error "_port_"-"_$G(lzb)_":"_$G(lzc) Q
WAITR U TCP D REPT H:0 3 G WAITR:TCP'="",WAITG
REPT S I=1,COM=$G(^ZAA02GSCRVI)+1,^ZAA02GSCRVI=COM,A="",N=0 F  R *B:30 X ZC G RTO:'$T,RTC:zc I B=11 Q
 F  R *B:30 X ZC G RTN:'$T,RTC:zc Q:B=28  S:B>31 A=A_$C(B) I B=13!($L(A)=250) S ^ZAA02GSCRVI(COM,I)=A,I=I+1,A="" I I=2 S N=$P($P($P(^(1),"MSH|",2),"|",9),$C(13))
 S ^ZAA02GSCRVI(COM,I)=A
 W *11,"MSH|^~\&|||||||ACK|01234"_$C(13)_"MSA|AA|"_N_$C(13,28,13),! J:1 EXT^ZAA02GSCRVT1 Q
RTN S ^ZAA02GSCRVI(COM)=9 W *11,"MSH|^~\&|||||||ACK|01234"_$C(13)_"MSA|AR|"_N_$C(13,28,13),! Q
RTO Q
RTC C TCP S TCP="" Q
 ;
IP S IP=$G(^ZAA02GSCRVTL("TCP")),PORTO=$G(^("PORTO")),PORTI=$G(^("PORTI")) Q
TCPS S TRY=0,ip="" G INITS ;  SERVER
TCPC S TRY=0,ip=IP G INITS ;  CLIENT
INITS G:$G(^ZAA02G("OS"))'="MSM" INITM C 56 O 56:(:0):"TCP" U 56 S KEY=$KEY,TCP=""
INITS2 W /SOCKET(ip,port) S %xddb=$KEY,lza=$ZA,lzb=$ZB,lzc=$ZC,ZC="s zc=$ZC"
 I lzc&(lzb=-8) S TRY=TRY+1 G INITS2:TRY<3 Q
 I lzc S TRY=TRY+1 G INITS:TRY<3 Q
 S TCP=56 Q
INITM S TCP="|TCP|"_port,ZC="s zc=$za\4#1" O TCP:(ip:port:"AS") Q:ip'=""
 U TCP R *%xddb Q
 ;
ERROR S B="B="_^ZAA02G("ERRORR"),@B,^ZAA02GSCRVTL("ERROR",$H)=B H 5 C:TCP'="" TCP S TCP="" Q
HALT S ^ZAA02GSCRVTL("STOPPED",$H)="" H
 ;
 ;                                       
FORMAT ;  REPORT & ACK
 ;MSH|^~\&|ZAA02GSCRIPT|FACILITY|||TIMESTAMP|MESSTYPE|CONTROL ID
 ;PID|||MRN||NAME|........|19TH PIECE = SSAN
 ;ORC|RE
 ;OBR|||ACCESSION#|SERVICE ID|......|22.STATUS CHANGE|26.P|32.DOCTOR
 ;OBX|SEQ #|FT|STUDY||FINDINGS
 ;
 ;MSH|^~\&|||||||ACK|
 ;MSA|AA|CONTROL ID        AA=ACCEPT, AE=ERROR, AR=REJECTED
 ;
 ;  ORDERS
 ;MSH|^~\&||||||8.ORU|9.MESSAGE CONTROL ID
 ;PID|||4.MRN||6.NAME||8.DOB|SEX|........|19.SSAN
 ;OBR|||4.ACCESSION#|5.EXAM CODE|||8.EXAM DATE&TIME|....|17.DOCTOR
 ;
 ;
 ;INSERTED INTO ROUTINE  ^APPCH 
 ;S ^ZAA02GSCRVP=$G(^ZAA02GSCRVP)+1,^ZAA02GSCRVP(^ZAA02GSCRVP)=":"_SDATE_":"_K_"/"_K1 J POSTAP^ZAA02GSCRVT1 Q
 ;
 ;  setup for interface to TALK TECH box  - ^APP3
 ;S ^ZAA02GSCRVA=$G(^ZAA02GSCRVA)+1,^ZAA02GSCRVA(^ZAA02GSCRVA)=EZ_":"_K_"/"_K1_":"_DATE_":"_$G(RFG) J POSTAP^ZAA02GSCRVT1
 ;
POSTAP S A=^ZAA02G("ERROR")_"=""POSTE^ZAA02GSCRVT1""",@A
 S A="" F  S A=$O(^ZAA02GSCRVA(A)) Q:A=""  S B=^(A) K ^(A) S $P(B,":",2,3)=$P(B,":",3)_":"_$P(B,":",2) D POST(A,B)
 S A="" F  S A=$O(^ZAA02GSCRVP(A)) Q:A=""  S B=^(A) K ^(A) D POST(A,B)
 J OUTPUT^ZAA02GSCRVT1 Q
 ;
POST(ACCESSN,TR) ; ENTRY FROM POSTING ROUTINE FOR ADS
 N (ACCESSN,TR) S SITE="ADS",EXAMDATE=$P(TR,":",2) S:$L(EXAMDATE)=6 EXAMDATE="19"_EXAMDATE
 S K=$P(TR,":",3),K1=1,RFG=$P(TR,":",4,99) Q:K=""  I K["/" S K1=$P(K,"/",2),K=+K
 S GRG=$G(^GRG(K)),PTG=$G(^PTG(K,K1)),MR=K S:K1>1 MR=MR_","_K1
 S NAME=$P(PTG,":",3) S:NAME="" NAME=$P(GRG,":",7)
 s DOB=$P(PTG,":",12) S:$L(DOB)=6 DOB="19"_DOB
 S TT=$P(PTG,":",12),EXAM=$P(TR,":",6) S:EXAM]"" EXAM=EXAM_"^"_$P($G(^INPC(EXAM)),":",2) S:EXAM="" EXAM="NA^NOT AVAILABLE"
 S RF=$P(RFG,":"),(RFF,RFL,RFCSZ)="" I RF="" S RF=$P(PTG,":",16) I RF]"" S RFG=$G(^RFG(RF))
 I RF]"" D RFF^ZAA02GADS1 S RFF=T D RFL^ZAA02GADS1 S RFL=T ;,RFA1=$P(RFG,":",4),RFA2=$P(RFG,":",5),RFCSZ=$P(RFG,":",6)_"  "_$P(RFG,":",7),RFFAX=$P(RFG,":",21)
 I RF]"" S RFER=RF_"^"_RFF_" "_RFL_" "_RFCSZ
 G QUEUE
POSTE S A="A="_^ZAA02G("ERRORR"),@A,^ZAA02GSCRVTL("ERROR",$H)="POST ERROR - "_A_" "_B H 5 G POSTAP
 ;
SCHED ; CALLED FROM SCHEDULING ACTIVITIES IN VA CODE
 ;
 ;   S ^ZAA02GSCRVTS=$G(^ZAA02GSCRVTS)+1,^ZAA02GSCRVTS($G(^ZAA02GSCRVTS))=SC_"`"_SD_
 ;     "`"_SDY_"`"_DFN_"`"_$H ; added in SDM1A+8 - sumter
 ;                                       SDAM
 S X="",SITE="SUMTER"
SCHED1 S X=$O(^ZAA02GSCRVTS(X)) G:X="" SCHED2 S x=^(X),D=$P(x,"`",5) I +D=+$H,$P(D,",",2)+180<$P(D,",",2) G SCHED1
 S EXAM=$P($G(^SC($P(x,"`"),"S",$P(x,"`",2),1,$P(x,"`",3),0)),"^",4)
 S R=$G(^DPT($P(x,"`",4),0)),(SSAN,MR)=$P(R,"^",9),SEX=$P(R,"^",2),DOB=$P(R,"^",3),NAME=$P(R,"^") S:$G(^(1709010))]"" MR=+^(1709010)
 S ACCT="" ; FOR NOW
 S EXAMDATE=$P(x,"`",2),EXAMDATE=$E(EXAMDATE)+17*1000000+$E(EXAMDATE,2,7),DOB=$E(DOB)+17*1000000+$E(DOB,2,7)
 S ACCESSN=X,UNIQUE=X D QUEUE K ^ZAA02GSCRVTS(X) G SCHED1
SCHED2 I $O(^ZAA02GSCRVTS("")) H 60 G SCHED
 Q
 ;
QUEUE N X D DATE^ZAA02GSCRER
 S NAME=$TR(NAME,", ","^^")
 S (^ZAA02GSCRVT,ORDER,ACCESSN)=$G(^ZAA02GSCRVT)+1
 D ORDER
 S A=$G(^ZAA02GSCRVT) ; F A=A:-1:A-200 I $D(^ZAA02GSCRVTL("AA",A)),$P(B,"|",3,8)=$P(^(A),"|",3,8),$P(B,"|",10,11)=$P(^(A),"|",10,11) G Q1
 ;  note - making mess # and assession # = queue #
 S ^ZAA02GSCRVT(ORDER,1)=D(1) F J=2:1 Q:'$D(D(J))  S ^(J)=D(J)
 I $D(^ZAA02GSCRVTL("STOP")) Q
 L +^ZAA02GSCRVT:0 I  L -^ZAA02GSCRVT J OUTPUT^ZAA02GSCRVT1
 Q
 ;
ORDER ; fill hl7 records with order info
 F J=1:1 S RECD=$P($T(ORDDATA+J),";",2,99) Q:RECD=""  S D(J)="" F L=1:1:$L(RECD,";") S A=$P(RECD,";",L),B=$P(A,",",3,9),C=$P(A,",",2),A=$P(A,",") S B="B="_$S(B="":"A",B?1.A:"$G("_B_")",1:B),@B,$P(D(J),"|",C)=B
 Q
ORDDATA ;
 ;MSH,1;^~\&,2;,3,$$TYP;,4,SITE;,7,$$DT(DT);ORU,9;,10,ORDER
 ;PID,1;,4,MR;,6,NAME;,8,DOB;,9,SEX;,19,ACCT;,20,SSAN
 ;OBR,1;,4,ACCESSN;,5,EXAM;,8,EXAMDATE;,17,RFER
 ;
TYP() Q "ZAA02GSCRIPT"
SITE() Q "SUMTER"
DT(DT,TM) I $G(TM) Q $P(DT,"/",3)+1900*100+$P(DT,"/")*100+$P(DT,"/",2)*10000+$TR(TM,":")
 Q $P(DT,"/",3)+1900*100+$P(DT,"/")*100+$P(DT,"/",2)
 ;
 ;
STATUS W !!!,"VOICE TECHNOLOGY INTERFACE STATUS",!!! I $O(^ZAA02GSCRVT(""))="" W "No Records in Queue",!!!
 E  W $G(^ZAA02GSCRVT)-$O(^ZAA02GSCRVT(""))+1," records in queue",!!!
 L ^ZAA02GSCRVT:0 E  W "Interface is currently RUNNING",!!!,"Do you want to stop it? (Y/N) " R A#1 S:A="Y"!(A="y") ^ZAA02GSCRVTL("STOP")=$H G STAT2
 L  D TCP O TCP::60 E  W "Interface is not running, but Port ",TCP," is not available - cannot be started" H 5 G STAT2
 C TCP W "Interface is not running - Do you want to start it now? (Y/N) " R A#1 I A="Y"!(A="y") J ^ZAA02GSCRVT W !!!,"process started" H 5 G STAT2
STAT2 Q
MENUSTAT D STATUS G MNUSP^MNU
 ;
EXT S A=^ZAA02G("ERROR")_"=""ERROR^ZAA02GSCRVT""",@A D DATABASE^ZAA02GSCRPW,PARAM^ZAA02GSCRPW S A=""
EXT2 K NAME S A=$O(^ZAA02GSCRVI(A)) Q:A=""  G:$G(^(A)) EXT2 S ^(A)=1 M ^ZAA02GSCRVTL("REC_REP",A)=^ZAA02GSCRVI(A)
 F J=1:1:5 I $P($G(^ZAA02GSCRVI(A,J)),"|")="OBR" S OB=^(J),JJ=-$P(OB,"|",4) Q
 G EXT2:$G(JJ)=""
 S (P,O)="" F J=1:1:5 S:$P($G(^ZAA02GSCRVTL("SENT_ORDERS",JJ,J)),"|")="OBR" O=^(J) S:$P($G(^(J)),"|")="PID" P=^(J)
 D EXT3 S GLOB="^ZAA02GTMP($J)" K @GLOB S LN=1
 S (L,B)="" F  S B=$O(^ZAA02GSCRVI(A,B)) Q:B=""  S C=^(B) I $P(C,"|")="OBX" S L=L_$P(C,"|",6),F=$P(C,"|",6)="" D:$L(L)>80!F EXT1
 S @GLOB@(LN)=L,LN=LN+1,B=.03 I LN>1 F  S B=$O(@ZAA02GWPD@(B)) Q:B=""  I ^(B)["*" D EXTINS Q
 D EXT5
 L  S ^ZAA02GSCRVTL("REP",DOC)=$H_","_A K ^ZAA02GSCRVI(A) H 5 G EXT2
EXTINS S C=$O(^(B)) S:C="" C=B+100,^(C)=B_$C(1,1,1) S D=C-B\LN,J=$O(@GLOB@("")),$P(@ZAA02GWPD@(B),$C(1),4)=@GLOB@(J) F  S J=$O(@GLOB@(J)) Q:J=""  S @ZAA02GWPD@(B+D)=B_$C(1,1,1)_@GLOB@(J),B=B+D
 S $P(@ZAA02GWPD@(C),$C(1))=B Q
EXT1 F J=1:1:$L(L)\65 S C=$E(L,1,65),C=$P(C," ",1,$L(C," ")-1),L=$E($P(L,C,2,9),2,999) S @GLOB@(LN)=C,LN=LN+1
 I F S @GLOB@(LN)=L,L="",LN=LN+1 S:C'="" @GLOB@(LN)="",LN=LN+1
 Q
 ; mr,sitec,patient,dob,ds,template,proc1,consult
EXT3 N A,F,L D DATE^ZAA02GSCRER,TMPL^ZAA02GSCRER
 S (INP("TR"),TRANS)="VT",(OSET,OCOUNT,OT)="",(INP("DD"),INP("DT"))=DT,INP("TM")=TM,ZAA02G("RON")=$C(27)_"AAAA",XF=$C(1)
 S UP="ABCDEFGHIJKLMNOPQRSTUVWXYZ",LC="abcdefghijklmnopqrstuvwxyz",TIME=$P($H,",",2),STMC=0,ZAA02GWPD="@ZAA02GSCR@(DIR,DOC)",DIR="TRANS"
 ;
 S INP("MR")=$P(P,"|",4),PAT=$P(P,"|",6),INP("DOB")=$$EXT4($P(P,"|",8))
 I $P(ZAA02GSCRP,";",3)=7 S:INP("MR")?9N K=INP("MR"),INP("MR")=$E(K,1,3)_"-"_$E(K,4,5)_"-"_$E(K,6,9) D GET^ZAA02GSCRSRH
 I $P(ZAA02GSCRP,";",3)=2,PAT="" S K=INP("MR"),K1=1 S:K["/" K1=$P(K,"/",2),K=+K I $D(^PTG(K,K1)) S PAT=$P(^(K1),":",3) S:PAT="" PAT=$P(^GRG(K),":",7)
 S:PAT["^" PAT=$TR($P(PAT,"^")_","_$TR($P(PAT,"^",2,9),"^"," "),LC,UP) S INP("PATIENT")=PAT
 S INP("DS")=$$EXT4($P(O,"|",8)),INP("PROC1")=$P($P(O,"|",5),"^",2),INP("PROC2")=""
 S INP("CONSULT")=$P($P(O,"|",17),"^"),INP("PROVIDER")=$P(OB,"|",33)
 F A="PATIENT","DOB","MR","DS","PROC1","PROVIDER","CONSULT" S:INP(A)="" INP(A)="?"
 S (REP,INP("TEMPLATE"))="blank",INP("SITEC")="O",INP("DIST")="A"
 S PTYPE=4,PDEV=5 ; THESE CAN BE CHANGED IN PARAM
 I $D(@ZAA02GSCR@("PARAM","VT")) X ^("VT") ; site params here
 ; S X=INP("CONSULT") D FAX^ZAA02GSCRER2 ; CHECKS CONSULT
 ;
 D NEW^ZAA02GSCRER1 Q
 ;
EXT5 S ODOC=DOC N A,DOC S DOC=ODOC,P=PTYPE S:0 INCOMP=1 D FILE^ZAA02GSCRER
 ; I ZAA02GSCRP["W" S STMC=STMC_",A" D MAMMO^ZAA02GSCMR K TYPS,EDIT
 S DV=PDEV,INP("WORK")="",TRTYPE="" D STATS^ZAA02GSCRER2 D:P<3 PR2^ZAA02GSCRER2
 Q
 ;
EXT4(X) Q $E(X,5,6)_"/"_$E(X,7,8)_"/"_$E(X,3,4)
 ;
TESTREC D IP Q:IP=""  S port=PORTO D TCPS Q:TCP=""
 U TCP
TESTR1 R *R:30 I R'[11 G TESTR1
 S R="" F  R *B:25 G:'$T TESTR1 Q:B=28  S R=R_$S($L(R)<255:$C(B),1:"")
 G:R'["MSH|" TESTR1  S B=$P($P($P(R,"MSH|",2),"|",9),$C(13)) G:B="" TESTR1
 W *11,"MSH|^~\&|||||||ACK|01234"_$C(13)_"MSA|AA|"_B_$C(13,28),! G TESTR1
 Q
 ;
AAA S (A,B,C)="",D=980930 F  S A=$O(^INVAPP(1,D,A)) Q:A=""  F  S B=$O(^INVAPP(1,D,A,B)) Q:B=""  S C=$P(^(B),":",5) W A," ",B," ",C,! S ^ZAA02GSCRVP=^ZAA02GSCRVP+1,^ZAA02GSCRVP(^ZAA02GSCRVP)=":980930:"_$TR(C,"^","/")
 Q
