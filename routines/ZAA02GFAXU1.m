ZAA02GFAXU1 ;PG&A,ZAA02G-FAX,1.36,UTILITY - SIGNATURES & GRAPHICS;24NOV93 1:33P;;;17NOV99  17:18
 ;
 ; THIS FIRST SECTION READS DOS FILE FROM SCANNER SOFTWARE
 ; ASSUMES THAT THE FILE IS IN FAX FORMAT
 ;
 ;
OTHER R "FILE-",A,!,"NAME IT-",GR,! G F1
 ;
F D:'$D(ZAA02GP) FUNC^ZAA02GDEV S %R=3,%C=1 W @ZAA02GP,ZAA02G("CS") S %R=5,%C=5 W @ZAA02GP,"FILE (C:\SCANMAN\GRAY\?.BFX) -" S X="",%C=36 X ^ZAA02GREAD S GR=X Q:X=""
 W ! S A="C:\SCANMAN\GRAY\"_GR_".BFX"
F1 O 51:(A:::::"") K ^ZAA02GFAXGR("BFX",GR)
 F J=1:1 U 51 R A#150 Q:$ZC  S ^ZAA02GFAXGR("BFX",GR,J)=A U 0 W "."
 C 51
 ;
LE ; USE THIS FOR SIGNATURES
 ; THIS CODES SCANS THE DOCUMENT FOR $C(16) AND DOUBLES THEM UP 
 ; ALSO SCANS FOR SEPARATE LINES AND BREAKS THEM INTO INDIVIDUAL SCAN LINES
 S D="^ZAA02GFAXGR(""BFX"","""_GR_""")" D LE1
 Q
LE1 S ST="" F J=1:1 S A=ST,ST=$O(@D@(ST)) Q:ST=""  I $TR(^(ST),$C(0))'="" Q
 S B=$C(0,0,0),C=1,G="" K ^ZAA02GFAXGR("TEMP",$I) F J=1:1 S A=$O(@D@(A)) Q:A=""  S M=^(A) D:M[$C(16) LE2 S M=G_M,E=$L(M,B),G=B_$P(M,B,E) F E=1:1:$L(M,B)-1 S F=$P(M,B,E) I F'="" W "."  S ^ZAA02GFAXGR("TEMP",$I,C)=B_F,C=C+1
 S:$TR(G,$C(0))'="" ^ZAA02GFAXGR("TEMP",$I,C)=G K ^ZAA02GFAXGR("GR",GR) S A="" F J=1:1 S A=$O(^ZAA02GFAXGR("TEMP",$I,A)) Q:A=""  S M=^(A) S:$E(M,1,6)=$C(0,0,0,0,0,0) M=$E(M,5,255) S:$E(M,1,4)=$C(0,0,0,0) M=$E(M,3,255) S ^ZAA02GFAXGR("GR",GR,J)=M_$C(0)
 S ^ZAA02GFAXGR("GR",GR)=J-1 Q
LE2 F K=1:2:$L(M,$C(16))-1*2 S $P(M,$C(16),K)=$P(M,$C(16),K)_$C(16) W "."
 Q
 ;
TEST D:'$D(ZAA02GP) FUNC^ZAA02GDEV S Y="10,3\SRHL\3\AVAILABLE GRAPHICS",Y(0)="\EX\^ZAA02GFAXGR(""GR"")\TO" D ^ZAA02GPOP Q:X=""  S GR=X
 K ^ZAA02GFAXGR(10) S ^(10,10)="Test fax of ZAA02G-FAX graphic image: "_GR,^(20)="",^(30)="~FAX GRAPHIC=^ZAA02GFAXGR(""GR"","""_GR_""")"
 S FAXPARAM="PG&A`708 619 7530`ZAA02G-FAX graphic test``6197530`PG&A`1``N```N`````DOC",ZAA02GWPD="^ZAA02GFAXGR(10)"
 D QUEUE^ZAA02GFAXQ Q
 ;
COPY D:'$D(ZAA02GP) FUNC^ZAA02GDEV S %R=3,%C=1 W @ZAA02GP,ZAA02G("CS") S %R=5,%C=5 W @ZAA02GP,"Copy What Graphic? -" S X="",%C=26 X ^ZAA02GREAD S GR=X Q:X=""
 I $D(^ZAA02GFAXGR("GR",GR))=0 W "  Not found" H 3 G COPY
 S %R=7,%C=5  W @ZAA02GP,"Drive A (5.25) or B (3.5)? " S X="",%C=31 X ^ZAA02GREAD S DR=X Q:X=""
 S %R=9,%C=5  W @ZAA02GP,"Load floppy and press RETURN " S X="",%C=34 X ^ZAA02GREAD
 D INT^%D,INT^%T S F=DR_":"_GR_".TFX"
 O 51:(F:"W") U 51 W %TIM1,"  ",%DAT1,!,"ZAA02G-FAX GRAPHIC - ",GR," IN GLOBAL ^ZAA02GFAXGR",!
 W "^ZAA02GFAXGR",!
 F J=1:1 S A=$O(^ZAA02GFAXGR("GR",GR,A)) Q:A=""  U 51 W "^ZAA02GFAXGR(""GR"",""",GR,""",",A,")",!,^(A) U 0 W "."
 U 51 W "*",!,"*",!,"**",!,"**",!! U 0 W "done" C 51 H 2
