ZAA02GSCRMS ;PG&A,ZAA02G-SCRIPT,2.10,DATABASE EDIT;20JAN95 1:25P;;;Wed, 27 Nov 2013  11:01
 ;Copyright (C) 1984, Patterson, Gray & Associates, Inc.
 ;
T5 G:TRTYPE<3 NA S Y="Transcriptionist\Provider\Exam Types\Unit\Work Types\Site\Distribution Lists\Referral\Batch Definition\Quit\;TPEUWSDRBQ;S Y S T E M   F I L E S"
 S TC=32,TR=7 D SEL^ZAA02GSCRIPT Q:J'<NE  D @$P("TRANS\PROV\EXAM\UNIT\WORK\SITE\DIST\REF\BAT","\",J) G T5
 ;
T9 G:TRTYPE<3 NA S Y="Basic Parameters\Configuration Parameters\Default Report Format\Header/Footer Formats\Set Default Entity\Edit Helps Screens\Quit\;BCDHSEQ;C O N F I G U R A T I O N   O P T I O N S"
 S TC=26,TR=8 D SEL^ZAA02GSCRIPT Q:J'<NE  D @$P("BASIC\CONFIGP\DEFAULTS\HDFR\CONFIG\HELP","\",J) G T9
NA G NA^ZAA02GSCRIPT
 ;
TRANS S A="TRANSCRIPTIONIST'S PARAMETERS",SNL=9,ZAA02GSCRT=$S($D(@ZAA02GSCR@("PARAM","USER FILE")):^("USER FILE"),1:ZAA02GSCR) K CODE G FORM
TCODE S opi="FS;PASSWORD;OCODE" X op Q
TCODE1 Q:$D(OCODE)+$D(CODE)'=2  K:OCODE]""&(CODE'=OCODE) @ZAA02GSCRT@("PARAM","CODE",OCODE) S:CODE]""&$D(ID) @ZAA02GSCRT@("PARAM","CODE",CODE)=ID K OCODE,CODE Q
VCODE Q:X=""  I $G(@ZAA02GSCRT@("PARAM","CODE",X))=""!($G(^(X))=ID) Q
 Q
PROV S A="P R O V I D E R   P A R A M E T E R S",SNL=1 G FORM
PCODE S opi=2,opi(1)="FS;ZAA02GVIEWP;OTCODE",opi(2)="FS;ESPASS;OECODE" X op Q
PCODE1 I $D(OTCODE)+$D(TCODE)=2  K:OTCODE]""&(TCODE'=OTCODE) @ZAA02GSCR@("PARAM","TCODE",OTCODE) S:TCODE]""&$D(ID) @ZAA02GSCR@("PARAM","TCODE",TCODE)=ID K OTCODE,TCODE
 I 1 Q:$D(OECODE)+$D(ECODE)'=2  K:OECODE]""&(ECODE'=OECODE) @ZAA02GSCR@("PARAM","ECODE",OECODE) S:ECODE]""&$D(ID) @ZAA02GSCR@("PARAM","ECODE",ECODE)=ID K OECODE,ECODE Q
PLKUP Q:X=""  I '$D(@ZAA02GSCR@("PROV",X)),$D(^PVG(X)) S x=^PVG(X),@ZAA02GSCR@("PROV",X)=$TR($P(x,":",2),",","\")_"\"_$P(x,":",3)_"\\\\\\N\\\\N" K x
 I 1 Q
REF S A="R E F E R R A L   P A R A M E T E R S",SNL=7 D FORM
 K @ZAA02GSCR@("PARAM","REFA") S A="" F  S A=$O(@ZAA02GSCR@("PARAM","REF",A)) Q:A=""  S @ZAA02GSCR@("PARAM","REFA",$TR($P(^(A),"\")_", "_$P(^(A),"\",2)_" ",lc,uc))=A
 Q
UNIT S A="U N I T    P A R A M E T E R S",SNL=8 G FORM
SITE S A="S I T E   P A R A M E T E R S",SNL=11 G FORM
INCENT S A="I N C E N T I V E  P A Y  P A R A M E T E R S",SNL=21 G FORM
EXAM S A="TYPE OF EXAM PARAMETERS",SNL=23 G FORM
DIST S A="DISTRIBUTION LISTS",SNL=13,formHELP="DHP^ZAA02GSCRHP;D" G FORM
NCOPIES N I F I=18:-1:1 I $G(b(6000,I,1))'?.P Q:$P($G(b(1)),"\",2)=I  G NCP1
 I 1 Q
NCP1 S Y="20,8\RHL\1\\\The number of copies ("_+$P($G(b(1)),"\",2)_") does*,not match the number of*,defined lines below.*,*,Set # copies to match definition,Return with no changes",REFRESH="" D ^ZAA02GPOP
 S:X=5 $P(b(1),"\",2)=I G AP0^ZAA02GFORM4
COPY Q
CP N (X,TEMP) R "COPY FROM - ",AA,! Q
BAT S A="BATCH DEFINITION",SNL=17 G FORM
BATCMP G:eq="D" DELETE S J=@ZAA02GSCR@("PARAM","BATCH",ID),E="S E=E" F M=2,6 S I=$P(J,"\",M) D BATCMP1
 S $P(@ZAA02GSCR@("PARAM","BATCH",ID),"\",12)=E Q
BATCMP1 S E=E_"_$C(95)" Q:I=""  I I'="T"!($P(J,"\",7)="") S E=E_"_$G(^ZAA02GWP(.9,DP,XDC,"""_$P("PATIENT,PROVIDER,MR,MR,CONSULT,DT",",",$F("NPMTAR",I)-1)_"""))" Q
 S I=$P(J,"\",7) S E=E_"_$S($D(^ZAA02GWP(.9,DP,XDC,""MR""))#2=0:"""",1:"
TT ; R "L-",I,! S E=""
 S DR=-1,(D,C,R)="",E=E_"$E(^(""MR"")," F J=1:1:$L(I)+1 S F=$E(I,J) D BAT1
 S E=E_")" Q
BAT1 I F+DR'=C!("LRX"[F),$L(D) S:DR>0 C=-C+1,D=-D+1 S:C=0 C="" S:D=0 D="" S:F="X" C="$L(^(""MR""))-"_(R-1)
 I  S E=E_$S(DR<0:$S(C=D:C,1:D_","_C),1:$S(C=D:"$L(^(""MR""))"_C,1:"$L(^(""MR""))"_D_",$L(^(""MR""))"_C))_")"_$S($E(I,J+1,99)?.E1N.E:"_$E(^(""MR""),",1:""),D=""
 S:F="X" F="" S:D="" (C,D)=F S C=F S:F?1N R=R+1
 I F]"","LR"[F S DR=$S(F="L":-1,1:1),D=""
 ; W J,?5,DR,?10,F,?15,C,?20,D,?25,R,?30,E,!
 Q
BASIC S A="B A S I C    P A R A M E T E R S",SNL=12 G FORM
WORK S A="W O R K   T Y P E   P A R A M E T E R S",SNL=10 G FORM
FAX S A="A U T O F A X   P A R A M E T E R S",SNL=3 D FORM I $D(@ZAA02GSCR@("PARAM","FAX")) S A=$P(^("FAX"),"\",9),$P(^ZAA02GFAXC("CONFIG"),"\",13)=$S(A="E":"LOGERR",A="A":"LOGALL",1:"NOLOG")_"^ZAA02GSCRFX"
 Q
CONFIG G CONFIG^ZAA02GSCRPW
HELP G HELP^ZAA02GSCRRD
HDFR G HDFR^ZAA02GSCRRD
COMM S A="COMMUNICATION PARAMETERS",SNL=24 G FORM
COMMHP1 S REFRESH="" D COMMHP1X G AP0^ZAA02GFORM4
COMMHP1X N (ZAA02G,ZAA02GP,X,REFRESH) S K=1 F J=48:1:57,65:1:90 S A="^ZAA02GSCR"_$TR($C(J),0,"") I $D(@A) S Y(K)=$P($G(@A@("PARAM","BASIC")),"|"),Z(K)=J,K=K+1
 S Y="20,4\RHLY\1",Y(0)="\EX\" D ^ZAA02GPOP S:$D(Z(X)) X=$C(Z(X)) S:X[";EX" X="" Q
COMMHP2 S opi="FD;RDENTITY;ENT",REFRESH="" X op D COMMHP2X G AP0^ZAA02GFORM4
COMMHP2X N (ZAA02G,ZAA02GP,ZAA02GSCR,ENT,REFRESH) S ENT=$S(ENT="":ZAA02GSCR,1:"^ZAA02GSCR"_$TR(ENT,0))
 S $P(ZAA02GSCRP,";",3)=$G(@ENT@("PARAM","LOOKUP")) D TMPL^ZAA02GSCRER
 F J=1:1:$L(TMPL,",") S Y($P(TMPL,",",J))=""
 S J=$G(@ENT@("PARAM","NEXT"))-30,L="" F J=J:1:J+30 I $O(@ENT@("TRANS",J,.011,""))'="" F  S L=$O(^(L)) Q:L=""  I $D(^(L))#2 S Y(L)=""
 S Y="20,4\RHLY\2\HEADER FIELDS",Y(0)="\EX\\TO" D ^ZAA02GPOP Q
WP G ^ZAA02GWPM5
CONFIGP S A="CONFIGURATION PARAMETERS",SNL=18,NODELETE=1 D FORM
 Q
CONFMOD I $G(@ZAA02GSCR@("PARAM","NOMODIFY"))<1,$G(^("XEDIT"))["NOMOD" S ^("XEDIT")=$P(^("XEDIT"),"D:OC")_$P(^("XEDIT"),"NOMOD^ZAA02GSCRMS ",2,99)
 I $G(@ZAA02GSCR@("PARAM","NOMODIFY"))>0,$G(^("XEDIT"))'["NOMOD" S ^("XEDIT")="D:OCOUNT NOMOD^ZAA02GSCRMS "_$G(^("XEDIT"))
 Q
CONFDEF S X="Job#,7,,Y\Patient,13\Acc #,6\Prov,4\Refr,6\Proc1,6\Proc2,6\ Date,8,/\Time,5,:\SC,2\TR#,3\St,1\" Q
DEFAULTS K DOC D ^ZAA02GSCRPS S:$D(NEW) @ZAA02GSCR@("PARAM","REPORTS")=NEW K NEW Q
 ;
FORM K ID S %R=1,%C=20,C=43-$L(A),C=$J("",C\2)_A_$J("",C\2) W @ZAA02GP,ZAA02G("HI"),C,!!,ZAA02G("CS")
 S lc="abcdefghijklmnopqrstuvwxyz",uc="ABCDEFGHIJKLMNOPQRSTUVWXYZ",sdf=""
 S SCR="ZAA02GSCRIPT",REFRESH="3:22",ZAA02Gform="1"_$S('$D(NODELETE):";;Delete",1:"") D ^ZAA02GFORM K formHELP,ZAA02GSCRT,NODELETE Q
PRINT R CC Q
DELETE D DELM Q:CC'="Y"  I SN=9,$D(OCODE) S C=OCODE K:C]"" @ZAA02GSCR@("PARAM","CODE",C)
 I SN=1 S C=$G(TCODE) K:C]"" @ZAA02GSCR@("PARAM","TCODE",C) S C=$G(ECODE) K:C]"" @ZAA02GSCR@("PARAM","ECODE",C) K:$G(ID)]"" @ZAA02GSCR@("PROV",ID)
 I $G(ID)]"" S C=$P("REF,UNIT,USER,WORK,SITE,EXAM,DIST",",",$F("  7  8  9 10 11 23 13 "," "_SN_" ")\3) I C]"" K @ZAA02GSCR@("PARAM",C,ID)
 W "...done" H 2 Q
 Q
PDEL D DELM
 Q
DELM W *13,ZAA02G("CL"),ZAA02G("LO"),"      Do you want to",ZAA02G("HI")," delete",ZAA02G("LO")," this item (",ZAA02G("HI"),"Y or N",ZAA02G("LO"),") - ",ZAA02G("HI") R CC#1 S:CC?1L CC=$C($A(CC)-32) Q
 ;
DEVICE S DV=$P(@ZAA02GSCR@("PARAM","BASIC"),"|",6+DV) I DV'="" S OFF=5 O DV::0 S:'$T DV=" DEVICE IS BUSY" Q
 Q
 ;
INC S Y="30,10\RHL\1\Types of Work - Select One\\None Defined\",Y(0)="\EX\" I $D(@ZAA02GSCR@("PARAM","WORK")) S Y(0)=Y(0)_"@ZAA02GSCR@(""PARAM"",""WORK"")\$P(@ZAA02GSCR@(""PARAM"",""WORK"",TO),$C(92))_""    ""_$P(^(TO),$C(92),2)"
 D ^ZAA02GPOP S:X[";EX" X="" I X]"",$D(@ZAA02GSCR@("PARAM","WORK",X))#2 S WORKT=$P(@ZAA02GSCR@("PARAM","WORK",X),"\")_"\"_$P(^(X),"\",3)
 Q
 ;
