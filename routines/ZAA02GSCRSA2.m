ZAA02GSCRSA2 ;PG&A,ZAA02G-SCRIPT,2.10,STATS - AD HOC REPORTS-3;;;;05JAN2000  10:47
 ;
PRX S WIDTH=79,OFF=0,PG=1 I TYPE=3 S WIDTH=300
 K HEAD S A="" F J=1:1 S A=$O(^ZAA02GSCRSA(II,.001,A)) Q:A=""  S HEAD(J)=$P(^(A),",",2),EX(J)=$P(^(A),",",4,9),L(J)=$P(^(A),",",3) S:$L(HEAD(J))>L(J) L(J)=$L(HEAD(J)) S:L(J)>$L(HEAD(J)) HEAD(J)=" "_HEAD(J)
 S NF=J-1,D=+$H D DATE S DT=D
 S A=OFF+L(1) F J=2:1:NF S A=A+2+L(J)
 I A>WIDTH F K=80:-2 F J=NF:-1:1 I L(J)>K S A=A-L(J)+K,L(J)=K G:A<WIDTH PR0
 ; I A+5<WIDTH F K=1:1:3 F J=1:1:NF S A=A+3,L(J)=L(J)+3 G:A+5>WIDTH PR0
PR0 S:A<WIDTH OFF=WIDTH-A\2+OFF
 S LN=$S(DEV=0:19,1:55) S A=OFF F J=1:1:NF S F(J)=A,A=A+L(J)+2,T(J)=""
 I TYPE=3 D INITSHT
 D PR1 K ^ZAA02GSCRSA(II) I DEV'=0 S DONE=1 Q
 K A,B,C,D,E,F,II,WIDTH,OFF,PG,NL,HEAD,DT,NF,TITLE,TITLE2,DEV Q
PR1 K J I TYPE=1 W:$Y #
 S (B,C,E,F)="",A=.009 I NL=1 F  S A=$O(^ZAA02GSCRSA(II,A)) Q:A=""  S R=^(A) D PR2
 I NL=2 F  S A=$O(^ZAA02GSCRSA(II,A)) Q:A=""  S B="" F  S B=$O(^ZAA02GSCRSA(II,A,B)) Q:B=""  S R=^(B) D PR2
 I NL=3 F  S A=$O(^ZAA02GSCRSA(II,A)) Q:A=""  S B="" F  S B=$O(^ZAA02GSCRSA(II,A,B)) Q:B=""  F  S C=$O(^ZAA02GSCRSA(II,A,B,C)) Q:C=""  S R=^(C) D PR2
 I NL=4 F  S A=$O(^ZAA02GSCRSA(II,A)) Q:A=""  S B="" F  S B=$O(^ZAA02GSCRSA(II,A,B)) Q:B=""  F  S C=$O(^ZAA02GSCRSA(II,A,B,C)) Q:C=""  F  S E=$O(^ZAA02GSCRSA(II,A,B,C,E)) Q:E=""  S R=^(E) D PR2
 I NL=5 F  S A=$O(^ZAA02GSCRSA(II,A)) Q:A=""  S B="" F  S B=$O(^ZAA02GSCRSA(II,A,B)) Q:B=""  F  S C=$O(^ZAA02GSCRSA(II,A,B,C)) Q:C=""  F  S E=$O(^ZAA02GSCRSA(II,A,B,C,E)) Q:E=""  F  S F=$O(^ZAA02GSCRSA(II,A,B,C,E,F)) Q:F=""  S R=^(F) D PR2
 I A'="~" D:$D(J) TOT I '$D(J) S %R=22,%C=26 W:DEV=0 @ZAA02GP W "NO DATA FOUND FOR ITEMS SELECTED"
 I TYPE=1 W # D:DEV=0 CONT Q
 I TYPE=3 D DISP Q
PR2 G:TYPE=3 PR3 W:$Y>LN # D:$Y=0 HEAD Q:A="~"  F J=1:1:NF X EX(J) W ?F(J),$S(J>NL:$J(D,L(J)),1:$E(D,1,L(J))) I J>NL S T(J)=T(J)+D
 W ! Q
PR3 G:SNL>0 PR4 D SHTHD
PR4 F J=1:1:NF X EX(J) S @ZAA02GCALG@(J,SNL)=J>NL*5_"`"_$E(D,1,L(J)) I J>NL S T(J)=T(J)+D
 S SNL=SNL+1 Q
TOT G:TYPE=3 SHTOT W:$Y+1>LN # D:$Y=0 HEAD Q:A="~"  F J=1:1:NF I J>NL W ?F(J),$E("------------------",1,L(J))
 W ! F J=1:1:NF I J>NL W ?F(J),$J(T(J),L(J))
 W ! Q
SHTOT F J=NL+1:1:NF S @ZAA02GCALG@(J,SNL)="0`"_$E(" -----------------------",1,L(J)+1)_"`"
 S SNL=SNL+1 F J=NL:1:NF S @ZAA02GCALG@(J,SNL)="5`"_T(J)_"`"
 S SNL=SNL+1 Q
 ;
DISP K EX,SNL N (ZAA02G,ZAA02GP,ZAA02GCALG,SHT) S TR=3,BR=23,DIR="AD" D ENTRY^ZAA02GCAL01 Q
 ;
DATE I 'D S D="" Q
 S K=D>21608+305+D,Y=4*K+3\1461,D=K*4+3-(1461*Y)+4\4,M=5*D-3\153,D=5*D-3-(153*M)+5\5,M=M+2,Y=M\12+Y+140,M=M#12+1,Y=Y*100+M*100+D,D=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3) K K,M Q
TIME S TM=$P($H,",",2)\60,TM=$E(TM\60+100,2,3)_":"_$E(TM#60+100,2,3)  Q
MMDD S D=$E(D,5,6)_"/"_$E(D,7,8)_"/"_$E(D,3,4) Q
MMYY S D=$E(D,5,6)_"/"_$E(D,3,4) Q
 ;
HEAD I DEV=0 D:PG>1 CONT S %R=3,%C=1 W @ZAA02GP,ZAA02G("CS")
 I DEV'=0 X:$D(HDRX) HDRX W !?WIDTH-$L(DT)-5,DT,!!,?WIDTH-$L(TITLE)\2,TITLE,!! I TITLE2]"" W:$L(TITLE2)<WIDTH ?WIDTH-$L(TITLE2)\2 W $E(TITLE2,1,WIDTH),! W:$L(TITLE2)>WIDTH ?14,$E(TITLE2,WIDTH+1,255),!
 W ! F J=1:1:NF S D=HEAD(J) W ?F(J),$E(D,1,L(J))
 W ! F J=1:1:NF S D=HEAD(J) W ?F(J),$E("--------------------------",1,L(J))
 W ! S PG=PG+1 Q
CONT S %R=24 I A'="" S %C=20 W @ZAA02GP,"Press RETURN to continue, EXIT to quit"
 E  S %C=30 W @ZAA02GP,"Press RETURN to continue"
 R D#1 X ZAA02G("T") S:$E(XX,$F(XX,ZF))="E" (A,B,C,E,F)="~" Q
 ;
INITSHT I $D(^ZAA02GCALC(98,"AD"))=0 S ^("AD")="AD HOC REPORTS\\\\\",^ZAA02GCALC("AD")=^ZAA02GCALC(98,"AD")
 F SHT=1:1 I '$D(^ZAA02GCALC("AD",SHT)) Q
 S ZAA02GCALG="^ZAA02GCALC(""AD"",SHT)",A="",DIR="" S @ZAA02GCALG=$E(TITLE,21,35)_"`"_$G(TRANS)_"`"_DT_"````0,75",^ZAA02GCALC(98,"AD",SHT)=@ZAA02GCALG
 S C="" F J=1:1:NF S C=C_$C(L(J)+1)
 F  S C=C_$C(8,8,8,8,8,8) Q:$L(C)>245
 F  S C=C_$C(8) Q:$L(C)=255
 S @ZAA02GCALG@("C",2)=C,SNL=0
 Q
SHTHD S SNL=1 F J=1:1:NF S D=HEAD(J) S @ZAA02GCALG@(J,SNL)="0`"_$S(J>NL:" ",1:"")_$E(D,1,L(J))_"`"
 S SNL=SNL+1 F J=1:1:NF S @ZAA02GCALG@(J,SNL)="0`"_$S(J>NL:" ",1:"")_$E("--------------------------",1,L(J))_"`"
 S SNL=SNL+2 Q
