ZAA02GSCRMC ;PG&A,ZAA02G-SCRIPT,2.10,MACROS;18NOV94 1:56P;;;19FEB97  12:50;Copyright (C) 1984, Patterson, Gray & Associates, Inc.
RMACRO ; MACRO ENTRY FOR ADDITIONAL REPORTS
 ; N (ZAA02GSCRP,ZAA02G,MC,MCG,ZAA02GSCR,REFRESH,ZAA02GP,SITE,TYPE,UP,LC,OXX,DOC,SB,DIR,ZAA02GWPD,XS,XF,W,ML,RM,MG,XSL,XFM,LTM,RTM)
RMACRO1 S (CPT,REFRESH,MC)="" S Y="12,5\YRHL\1\REPORT TEMPLATE LOOKUP MACRO",Y(0)="\EX",Y(1)="*",Y(2)="Enter name (or beginning letters) of report:*",Y(3)="*",Y(4)="",X=""
 D POP Q:X=""  Q:X[";EX"  S X=$TR(X,UP,LC) S OXX=X
RMENTRY D REPVAL^ZAA02GSCRHP Q:X=""  Q:X[";EX"  S ZAA02GINS="^WORKING($J,0)",F=0 K ^WORKING($J,0) S A=$O(@ZAA02GSCR@(106,X,.03)) F J=2:1 S A=$O(@ZAA02GSCR@(106,X,A)) Q:A=""  S:^(A)[$C(27) F=1 S ^WORKING($J,0,J)=$P(^(A),$C(1),4) ;_$C(1,70,1)
 I $D(@ZAA02GSCR@(106,X,.015)),^(.015)'=ZAA02G S ^WORKING($J,0,.015)=^(.015) D RMCONV K ^WORKING($J,0,.015)
 S KK=J-1,(MC,E)="",SXFM=XFM,OSB=+@ZAA02GWPD@(SB) D INSRT^ZAA02GWPV0 S SB=$O(@ZAA02GWPD@(OSB)) K OSB Q
RMCONV N (ZAA02G) S EP=1,ZAA02GWPD="^WORKING($J,0)" D CONV^ZAA02GWPCONV Q
 ;
POP N TL,TB,TP D ^ZAA02GPOP Q
 ;
 ; RLIST BRINGS BACK THE ORIGINAL LIST OF REPORTS
RLIST S %R=24,%C=1 W @ZAA02GP,ZAA02G("CL"),ZAA02G("LO"),"Erase this report first? (",ZAA02G("HI"),"Y",ZAA02G("LO")," or ",ZAA02G("HI"),"N",ZAA02G("LO"),") ",ZAA02G("HI") R X#1
 W *13,ZAA02G("CL") S:X?1L X=$C($A(X)-32) I X="Y" S X=.03,(CR,%R)=TL,PL=1,(TOP,SB)=$O(@ZAA02GWPD@(.03)) F J=1:1 S X=$O(^(X)) Q:X=""  S $P(^(X),XF,4)="" I %R'>BL W @ZAA02GP,ZAA02G("CL") S BOT=X,%R=%R+1
 G:'$D(OXX) RMACRO S X=OXX D RLIST1
 I X'[";EX" S INP("TEMPLATE")=X Q
 S X="" K OXX G RMACRO
RLIST1 ; N (ZAA02GSCRP,ZAA02G,MC,MCG,ZAA02GSCR,REFRESH,ZAA02GP,SITE,TYPE,UP,LC,X,INP,ZAA02GWPD,DIR,DOC,SB,XS,XF,W,ML,RM,MG,XSL,XFM,LTM,RTM)
 S (CPT,REFRESH,MC)="",%R=7,%C=10 D RMENTRY
 Q
 ;
AI ; ACCOUNT HISTORY FROM WITHIN EDIT MODE
 S CDOC=DOC N (ZAA02GSCR,ZAA02GSCRP,ZAA02G,ZAA02GP,MC,CDOC,REFRESH,INP) S K=INP("MR"),TOPP=7 D:$P(ZAA02GSCRP,";",3)=2 ^ZAA02GSCRAI D:$P(ZAA02GSCRP,";",3)'=2 MR^ZAA02GSCRVW S REFRESH=1,MC="" K CDOC Q
 ;
DEMO ; ADS DEMOGRAPHIC FROM WITHIN EDIT MODE
 S K=$G(INP("MR")),K1=1 D DEMOX S REFRESH=1,MC="" K K1 Q
DEMOX I $P(ZAA02GSCRP,";",3)=2 N (K,K1) S SWPOS="" D ^AI
 Q
 ;
SAVE ; SAVES CURRENT (MODIFIED) REPORT IN THE REPORT TEMPLATE FILE
 N (ZAA02GSCRP,ZAA02G,ZAA02GP,INP,ZAA02GSCR,ZAA02GWPD,DIR,DOC,MC,REFRESH,SDOC) S SDOC=DOC,REP=INP("TEMPLATE"),(REFRESH,MC)="" G:REP="" ME
 S Y="36,9\RHLW\1\\\WAIT" D ^ZAA02GPOP
 S A=$O(@ZAA02GSCR@(106,REP,.03)),(B,C)=A F J=1:1 S A=$O(@ZAA02GSCR@(106,REP,A)) Q:A=""  K ^(A)
 S A=.03 F J=1:1 S A=$O(@ZAA02GWPD@(A)) Q:A=""  S:$P(^(A),$C(1),4)'?." " C=B S @ZAA02GSCR@(106,REP,B+10)=B_$C(1,1,1)_$P(^(A),$C(1),4),B=B+10
 S B=C+10 F J=1:1 S B=$O(@ZAA02GSCR@(106,REP,B)) Q:B=""  K ^(B)
 S DIR=106,DOC=REP D SCAN^ZAA02GSCRRD S ^ZAA02GSCR("CHANGES",$H)="REPLACE: "_REP_","_$G(INP("TR"))
 S Y="30,7\RHL\1\\\*,Master Updated*,*, Press RETURN",Y(0)="\EX" D ^ZAA02GPOP G ME
ME S DIR="TRANS",DOC=SDOC,A=$D(@ZAA02GWPD@(.03)) K SDOC Q
 ;
RENAME ; RENAMES A REPORT
 N (ZAA02GSCRP,ZAA02G,ZAA02GSCR,ZAA02GWPD,REFRESH,ZAA02GP,INP,UP,LC,SB,MC,DOC) S SDOC=DOC,(REFRESH,MC)="",DOC=INP("TEMPLATE"),Y="6,7\YRHL\1\RENAME TEMPLATE FACILITY",Y(0)="\EX\\\\Press EXIT to Cancel"
 S X=$E(DOC_$J("",60),1,60),Y(1)="*",Y(2)="*Old Name: "_DOC,Y(3)="*",Y(4)="New Name: *"_X D ^ZAA02GPOP G ME:X="",ME:X[";EX" S REP=$TR(X,UP,LC)
 S DIR=106 G:REP=DOC ME S A="" F J=1:1 S A=$O(@ZAA02GSCR@(106,DOC,A)) Q:A=""  S:$D(^(A))#2 @ZAA02GSCR@(106,REP,A)=^(A)
 D DDEL^ZAA02GSCRRD K @ZAA02GSCR@(DIR,DOC),^(0,DOC) S ^ZAA02GSCR("CHANGES",$H)=DOC_" > "_REP_","_$G(INP("TR")) S DOC=REP D SCAN^ZAA02GSCRRD
 S INP("TEMPLATE")=REP,Y="30,9\RL\1\\\*,Master Renamed*,*, Press RETURN",Y(0)="\EX" D ^ZAA02GPOP G ME
 ;
ADDEND ; CREATE AN ADDENDUM FROM ANOTHER REPORT
 S ADDEND="ADDEND,"_DOC D COPY Q
COPY ; COPY FROM ANOTHER REPORT INTO THIS REPORT
 S K=$TR(INP("MR"),"-"),MC=DOC,COPY=1 D SELECT K COPY S REFRESH=1,MC="" Q:'$D(ZAA02GINS)
 K ^ZAA02GTWP($J) S A=.03,^ZAA02GTWP($J,1)="-----------COPY OF DOCUMENT "_(+$P(ZAA02GINS,",",2))_"------------" F KK=2:1 S A=$O(@ZAA02GINS@(A)) Q:A=""  S ^ZAA02GTWP($J,KK)=$S(@ZAA02GINS@(A)[$C(1):$P(^(A),$C(1),4),1:^(A))
 S ^ZAA02GTWP($J,KK)="-------------END OF COPY-------------"
 I $D(ADDEND),$G(@ZAA02GINS@(.011,"LOCKED"))'[ADDEND S ^("LOCKED")=$G(^("LOCKED"))_ADDEND,A=$O(@ZAA02GINS@(.03)) I A]"" S K=$J(A-.03\2,0,6)+.03,$P(^(A),$C(1))=K,^(K)=.03_$C(1,1,1)_"   THIS DOCUMENT HAS BEEN AMENDED BY ADDENDUM DOCUMENT - "_$G(DOC)
 S ZAA02GINS="^ZAA02GTWP($J)",IMP="",SXFM=XFM,SXSB=SB,KK=KK+1 D INSRT^ZAA02GWPV0 S SB=SXSB S:'$D(@ZAA02GWPD@(SB)) SB=TOP,CR=TL K SXFM,SXSB,IMP,SVSB,@ZAA02GINS,ZAA02GINS Q
SELECT N (INP,ZAA02GSCRP,ZAA02GSCR,ZAA02G,ZAA02GP,MC,K,ZAA02GINS,XX,COPY) K ZAA02GINS S TOPP=7,TOP=TOPP,DOC=0
 I $P(ZAA02GSCRP,";",3)'=2 S MR=K S:+MR=MR MR=MR_" " S SRC="@ZAA02GSCR@(""DIR"",3,MR)",E="I 10000000-I'=MC",OF=-1,OF1=10000000,EP="zzzzz" D ^ZAA02GSCRTR G SELECT1
 S A="" F J=1:1:99 S A=$O(^ZAA02GACT(K,"TRANS",A)) Q:A=""  I $D(@ZAA02GSCR@("TRANS",A))+$D(@ZAA02GSCR@("DIR",99,10000000-A))=0 K ^ZAA02GACT(K,"TRANS",A)
 S A="" F J=1:1:99 S A=$O(^ZAA02GACT(K,"TRANS",A)) Q:A=""  I $D(@ZAA02GSCR@("TRANS",A,.011))#2,$P(^(.011),"`",9)'=K K ^ZAA02GACT(K,"TRANS",A)
 S ID=K S %R=TOP-1,%C=1 W @ZAA02GP,ZAA02G("CL") D ACCT^ZAA02GSCRTR
SELECT1 S:DOC ZAA02GINS=ZAA02GSCR_"(""TRANS"","_DOC_")" Q
 ;
ADDR ; POP-UP ADDRESS BLOCK
 N (ZAA02G,ZAA02GP,REFRESH,INP,MC,ADDR,ZAA02GSCR,ZAA02GSCRP,DIR,DOC,ZAA02GWPD) S:'$D(ADDR) ADDR="CC1" F J=1:1:5 S B=$P(",A,B,C,D",",",J) I $G(INP(ADDR_B))]"" S ADR(J)=INP(ADDR_B)
 S SCR="ZAA02GSCRIPT",SNL=4,ZAA02Gform="0;;;N" D ^ZAA02GFORM S REFRESH=1,MC="",JJ=1 F J=1:1:5 S B=$P(",A,B,C,D",",",JJ) S:$G(ADR(J))]"" INP(ADDR_B)=ADR(J),@ZAA02GWPD@(.011,ADDR_B)=ADR(J),JJ=JJ+1
 F JJ=JJ:1:5 S B=$P(",A,B,C,D",",",JJ) K INP(ADDR_B),@ZAA02GWPD@(.011,ADDR_B)
 Q:$P(ZAA02GSCRP,";",3)=1
 I $D(@ZAA02GWPD@(.011)) F J=1,2 S $P(^(.011),"`",16+J)=$G(INP("CC"_J))
 K @ZAA02GWPD@(.011,"CC1"),^("CC2")
 Q
CCC D ADDR
 Q
CCC1 K @ZAA02GWPD@(.011,"CCC1"),INP("CCC1") S T="" F j=1:1:3 I $G(INP("CC"_j))]"" S T=T_INP("CC"_j)_", "
 I T]"" S (INP("CCC1"),^("CCC1"))="CC:         "_$E(T,1,$L(T)-2) Q
CCC2 K @ZAA02GWPD@(.011,"CCC2"),INP("CCC2") S T="" F j=4:1:6 I $G(INP("CC"_j))]"" S T=T_INP("CC"_j)_", "
 I T]"" S (INP("CCC2"),^("CCC2"))="CC:         "_$E(T,1,$L(T)-2) Q
 ;
UNDEF I X?1.L.N,$T(@X)]"" S MC=$P($T(@X),";",2) Q
 Q
 ;  
hist ;#D AI^ZAA02GSCRMC
demo ;#D DEMO^ZAA02GSCRMC
rename ;#D RENAME^ZAA02GSCRMC
report ;#D RMACRO^ZAA02GSCRMC
list ;#D RLIST^ZAA02GSCRMC
copy ;#D COPY^ZAA02GSCRMC
addend ;#D ADDEND^ZAA02GSCRMC
cc1 ;#S ADDR="CC1" D ADDR^ZAA02GSCRMC
cc2 ;#S ADDR="CC2" D ADDR^ZAA02GSCRMC
cc3 ;#S ADDR="CC3" D ADDR^ZAA02GSCRMC
cc4 ;#S ADDR="CC4" D ADDR^ZAA02GSCRMC
cc5 ;#S ADDR="CC5" D ADDR^ZAA02GSCRMC
cc6 ;#S ADDR="CC6" D ADDR^ZAA02GSCRMC
extra ;#S ADDR="EXTRA" D ADDR^ZAA02GSCRMC
incomp ;#S INCOMP=0,MC=""
 ;
 ;CCx      #S ADDR="CCx" D CCC^ZAA02GSCRMC - same but creates CCC
 ;REPLACE  #D SAVE^ZAA02GSCRMC
 ;
